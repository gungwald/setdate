NEW
10 REM  - /HOME/BILL/GIT/SETDATE/SETDATETIME.BAS - 03/02/2019 - 23h14
20 REM **************
30 REM SETDATETIME
40 REM BILL CHATFIELD
50 REM GPL3
60 REM **************
70 LET D$=CHR$(4)
80 LET LT$=CHR$(8)
90 LET DOWN$=CHR$(10)
100 LET UP$=CHR$(11)
110 LET RET$=CHR$(13)
120 LET D1$=CHR$(17) 
130 LET D2$=CHR$(18) 
140 LET NAK$=CHR$(21)   
150 LET RT$=CHR$(21)
160 LET ESC$=CHR$(27)
170 LET DLT$=CHR$(127)
180 LET BEEP$=CHR$(7)
190 LET TB$=CHR$(9)
200 LET BX=1
210 LET BY=2
220 DIM MNTHS$(12), MDAYS(12)
230 FOR I = 1 TO 12
240 READ MNTHS$(I), MDAYS(I)
250 NEXT
260 DATA JAN,31
270 DATA FEB,29
280 DATA MAR,31
290 DATA APR,30
300 DATA MAY,31
310 DATA JUN,30
320 DATA JUL,31
330 DATA AUG,31
340 DATA SEP,30
350 DATA OCT,31
360 DATA NOV,30
370 DATA DEC,31
380 PRINT D$;"PR#3" : REM TURN ON 80-COLUMN CARD
390 PRINT D1$       : REM SWITCH TO 40 COLUMNS FOR MOUSE CHARS
400 GOSUB 1270
410 GOSUB 1020
420 GOSUB 530
430 GOSUB 1620
440 GOSUB 1210
450 GOSUB 1440
460 PRINT NAK$ : REM TURN OFF 80-COL CARD
470 HOME
480 PRINT
490 IF K$ = RET$ THEN PRINT "SET SYSTEM DATE: "; : GOSUB 3790 : END
500 PRINT "CANCELLED SETTING OF DATE AND TIME."
510 END
520 REM->DRAWSCREEN
530 HTAB BX : VTAB BY
540 PRINT "         SET SYSTEM DATE & TIME"
550 PRINT
560 PRINT
570 PRINT
580 PRINT
590 HTAB BX
600 PRINT "             DAY  MONTH YEAR"
610 PRINT
620 HTAB BX
630 PRINT "        DATE: ";DAY$;" - ";MNTH$;" - ";YEAR$ 
640 PRINT
650 PRINT
660 HTAB BX
670 PRINT "             HOUR MIN MERIDIEM"
680 PRINT
690 HTAB BX
700 PRINT "        TIME: ";HOUR$;" : ";MINUTE$;"  ";AMPM$;"M"
710 PRINT
720 PRINT
730 PRINT
740 PRINT
750 HTAB BX
760 PRINT " RETURN - ACCEPT   "; : GOSUB 890 : PRINT "-D - GOTO YEAR"
770 HTAB BX
780 PRINT "    ESC - CANCEL   "; : GOSUB 890 : PRINT "-M - GOTO MONTH"
790 HTAB BX
800 PRINT "    TAB - NXT FLD  "; : GOSUB 890 : PRINT "-Y - GOTO YEAR"
810 HTAB BX
820 PRINT "   "; : GOSUB 940 : PRINT " - MOVE     "; : GOSUB 890 : PRINT "-H - GOTO HOUR"
830 HTAB BX
840 PRINT "      + - INC VAL  "; : GOSUB 890 : PRINT "-N - GOTO MINUTE"
850 HTAB BX
860 PRINT "      - - DEC VAL  "; : GOSUB 890 : PRINT "-R - GOTO MERIDIEM";
870 RETURN
880 REM->PRINTOPENAPPLE
890 INVERSE
900 PRINT CHR$(27);"A";CHR$(24);
910 NORMAL
920 RETURN
930 REM->PRINTARROWS
940 INVERSE
950 PRINT CHR$(27);"HJKU";CHR$(24);
960 NORMAL
970 RETURN
980 REM->FORMATMONTH
990 LET MNTH$ = MNTHS$(MNTH)
1000 RETURN
1010 REM->FORMATDATEANDTIME
1020 LET YEAR$ = STR$(YEAR)
1030 IF LEN(YEAR$) = 1 THEN LET YEAR$ = "0" + YEAR$
1040 GOSUB 990
1050 LET DAY$ = STR$(DAY)
1060 IF LEN(DAY$) = 1 THEN LET DAY$ = "0" + DAY$
1070 LET HOUR$ = STR$(HOUR)
1080 IF LEN(HOUR$) = 1 THEN LET HOUR$ = "0" + HOUR$
1090 LET MINUTE$ = STR$(MINUTE)
1100 IF LEN(MINUTE$) = 1 THEN LET MINUTE$ = "0" + MINUTE$
1110 RETURN
1120 REM->DECODEMONTH
1130 FOR I = 1 TO 12
1140 IF MNTHS$(I) = MNTH$ THEN MNTH = I : RETURN
1150 NEXT
1160 VTAB 24
1170 PRINT
1180 PRINT "INVALID MONTH NAME: " + MNTH$
1190 END
1200 REM->DECODEDATEANDTIME
1210 LET YEAR = VAL(YEAR$)
1220 GOSUB 1130
1230 LET HOUR = VAL(HOUR$)
1240 LET MINUTE = VAL(MINUTE$)
1250 RETURN
1260 REM->GETSYSTEMDATE
1270 LET DH = PEEK(49041): REM DATE HIGH BYTE
1280 LET DL = PEEK(49040): REM DATE LOW BYTE
1290 LET TH = PEEK(49043): REM TIME HIGH BYTE
1300 LET TL = PEEK(49042): REM TIME LOW BYTE 
1310 LET YEAR = INT (DH / 2)
1320 LET MNTH = ((DH/2 - INT(DH/2)) > 0) * 2 ^ 3 +  INT (DL / 32)
1330 LET DAY = DL
1340 IF DAY >= 128 THEN DAY = DAY - 128
1350 IF DAY >= 64  THEN DAY = DAY - 64
1360 IF DAY >= 32  THEN DAY = DAY - 32
1370 IF TH = 0  THEN HOUR = 12      : AMPM$ = "A"
1380 IF TH = 12 THEN HOUR = 12      : AMPM$ = "P"
1390 IF TH > 12 THEN HOUR = TH - 12 : AMPM$ = "P"
1400 IF TH < 12 THEN HOUR = TH      : AMPM$ = "A"
1410 LET MINUTE = TL
1420 RETURN 
1430 REM->SETSYSTEMDATEANDTIME
1440 IF YEAR >= 100 THEN Y2 = VAL(RIGHT$(STR$(YEAR),2))
1450 IF YEAR <  100 THEN Y2 = YEAR
1460 LET DH = Y2 * 2 + (MNTH >  = 8)
1470 LET DL = MNTH * 32 - 128 * (MNTH >  = 8) + DAY
1480 IF HOUR =  12 AND AMPM$ = "A" THEN TH = 0
1490 IF HOUR =  12 AND AMPM$ = "P" THEN TH = 12
1500 IF HOUR <> 12 AND AMPM$ = "P" THEN TH = HOUR + 12
1510 IF HOUR <> 12 AND AMPM$ = "A" THEN TH = HOUR
1520 LET TL = MINUTE
1530 POKE 49041,DH: REM DATE HIGH BYTE
1540 POKE 49040,DL: REM DATE LOW BYTE
1550 POKE 49043,TH: REM TIME HIGH BYTE
1560 POKE 49042,TL: REM TIME LOW BYTE
1570 RETURN 
1580 REM->SOUNDERROR
1590 PRINT BEEP$
1600 RETURN
1610 REM->ENTERDATEANDTIME
1620 REM->DAYDIGITONE
1630 HTAB 15 : VTAB 9
1640 GET K$
1650 IF "0" <= K$ AND K$ <= "3" THEN PRINT K$; : DAY$ = K$ + RIGHT$(DAY$, 1) : GOTO 1780
1660 IF K$ = " "             THEN GOTO 1780
1670 IF K$ = TB$        THEN GOTO 1980
1680 IF K$ = RT$ THEN GOTO 1780
1690 IF K$ = LT$  THEN GOTO 3640
1700 IF K$ = DOWN$ THEN GOTO 2940
1710 IF K$ = "+"             THEN GOSUB 3930 : PRINT DAY$; : GOTO 1630
1720 IF K$ = "-"             THEN GOSUB 4010 : PRINT DAY$; : GOTO 1630
1730 IF K$ = DLT$ THEN GOTO 3640
1740 GOSUB 4710
1750 GOSUB 1590
1760 GOTO 1630
1770 REM->DAYDIGITTWO
1780 HTAB 16 : VTAB 9
1790 GET K$
1800 IF K$ < "0" OR "9" < K$ THEN GOTO 1860
1810 LET FIRST$ = LEFT$(DAY$, 1)
1820 LET TEST$ = FIRST$ + K$
1830 LET TV = VAL(TEST$)
1840 IF 1 <= TV AND TV <= MDAYS(MNTH) THEN PRINT K$; : DAY$ = TEST$ : DAY = TV : GOTO 1980
1850 REM->DAYDIGITTWOKEYS
1860 IF K$ = " "             THEN GOTO 1980
1870 IF K$ = TB$        THEN GOTO 1980
1880 IF K$ = RT$ THEN GOTO 1980
1890 IF K$ = LT$  THEN GOTO 1630
1900 IF K$ = DOWN$ THEN GOTO 2940
1910 IF K$ = "+"             THEN GOSUB 3930 : PRINT LT$;DAY$; : GOTO 1780
1920 IF K$ = "-"             THEN GOSUB 4010 : PRINT LT$;DAY$; : GOTO 1780
1930 IF K$ = DLT$ THEN GOTO 1630
1940 GOSUB 4710
1950 GOSUB 1590
1960 GOTO 1780
1970 REM->MONTHCHARONE
1980 HTAB 20 : VTAB 9
1990 GET K$
2000 IF K$ = LEFT$(MNTH$, 1) THEN GOTO 2210
2010 IF K$ = "J" THEN MNTH$ = "JAN" : MNTH=1  : PRINT MNTH$; : GOTO 2210
2020 IF K$ = "F" THEN MNTH$ = "FEB" : MNTH=2  : PRINT MNTH$; : GOTO 2210
2030 IF K$ = "M" THEN MNTH$ = "MAR" : MNTH=3  : PRINT MNTH$; : GOTO 2210
2040 IF K$ = "A" THEN MNTH$ = "APR" : MNTH=4  : PRINT MNTH$; : GOTO 2210
2050 IF K$ = "S" THEN MNTH$ = "SEP" : MNTH=9  : PRINT MNTH$; : GOTO 2210
2060 IF K$ = "O" THEN MNTH$ = "OCT" : MNTH=10 : PRINT MNTH$; : GOTO 2210
2070 IF K$ = "N" THEN MNTH$ = "NOV" : MNTH=11 : PRINT MNTH$; : GOTO 2210
2080 IF K$ = "D" THEN MNTH$ = "DEC" : MNTH=12 : PRINT MNTH$; : GOTO 2210
2090 IF K$ = " "             THEN GOTO 2210
2100 IF K$ = TB$        THEN GOTO 2590
2110 IF K$ = RT$ THEN GOTO 2210
2120 IF K$ = LT$  THEN GOTO 1780
2130 IF K$ = DOWN$ THEN GOSUB 3290
2140 IF K$ = "+"             THEN GOSUB 4090 : PRINT MNTH$; : GOTO 1980
2150 IF K$ = "-"             THEN GOSUB 4150 : PRINT MNTH$; : GOTO 1980
2160 IF K$ = DLT$ THEN GOTO 1780
2170 GOSUB 4710
2180 GOSUB 1590
2190 GOTO 1980
2200 REM->MONTHCHARTWO
2210 HTAB 21 : VTAB 9
2220 GET K$
2230 IF K$ = MID$(MNTH$, 2, 1) THEN GOTO 2390
2240 LET FIRST$ = LEFT$(MNTH$, 1)
2250 IF FIRST$ = "A" AND K$ = "P" THEN MNTH$ = "APR" : MNTH=4 : PRINT "PR"; : GOTO 2390
2260 IF FIRST$ = "A" AND K$ = "U" THEN MNTH$ = "AUG" : MNTH=8 : PRINT "UG"; : GOTO 2390
2270 IF K$ = " "             THEN GOTO 2390
2280 IF K$ = TB$        THEN GOTO 2590
2290 IF K$ = RT$ THEN GOTO 2390
2300 IF K$ = LT$  THEN GOTO 1980
2310 IF K$ = DOWN$ THEN GOTO 3290
2320 IF K$ = "+"             THEN GOSUB 4090 : PRINT LT$;MNTH$; : GOTO 2210
2330 IF K$ = "-"             THEN GOSUB 4150 : PRINT LT$;MNTH$; : GOTO 2210
2340 IF K$ = DLT$ THEN GOTO 1980
2350 GOSUB 4710
2360 GOSUB 1590
2370 GOTO 2210
2380 REM->MONTHCHARTHREE
2390 HTAB 22 : VTAB 9
2400 IF K$ = RIGHT$(MNTH$, 1) THEN GOTO 2590
2410 LET TWO$ = LEFT$(MNTH$, 2)
2420 GET K$
2430 IF TWO$ = "MA" AND K$ = "R" THEN MNTH$ = "MAR" : MNTH=3 : PRINT K$; : GOTO 2590
2440 IF TWO$ = "MA" AND K$ = "Y" THEN MNTH$ = "MAY" : MNTH=5 : PRINT K$; : GOTO 2590
2450 IF TWO$ = "JU" AND K$ = "N" THEN MNTH$ = "JUN" : MNTH=6 : PRINT K$; : GOTO 2590
2460 IF TWO$ = "JU" AND K$ = "L" THEN MNTH$ = "JUL" : MNTH=7 : PRINT K$; : GOTO 2590
2470 IF K$ = " "             THEN GOTO 2590
2480 IF K$ = TB$        THEN GOTO 2590
2490 IF K$ = RT$ THEN GOTO 2590
2500 IF K$ = LT$  THEN GOTO 2210
2510 IF K$ = DOWN$ THEN GOTO 3290
2520 IF K$ = "+"             THEN GOSUB 4090 : PRINT LT$;LT$;MNTH$; : GOTO 2390
2530 IF K$ = "-"             THEN GOSUB 4150 : PRINT LT$;LT$;MNTH$; : GOTO 2390
2540 IF K$ = DLT$ THEN GOTO 2210
2550 GOSUB 4710
2560 GOSUB 1590
2570 GOTO 2390
2580 REM->YEARDIGITONE
2590 HTAB 26 : VTAB 9
2600 GET K$
2610 IF "0" <= K$ AND K$ <= "9" THEN PRINT K$; : YEAR$ = K$ + RIGHT$(YEAR$, 1) : GOTO 2740
2620 IF K$ = " "             THEN GOTO 2740
2630 IF K$ = TB$        THEN GOTO 2940
2640 IF K$ = RT$ THEN GOTO 2740
2650 IF K$ = LT$  THEN GOTO 2390
2660 IF K$ = DOWN$ THEN GOTO 3640
2670 IF K$ = "+"             THEN GOSUB 4210 : PRINT YEAR$; : GOTO 2590
2680 IF K$ = "-"             THEN GOSUB 4280 : PRINT YEAR$; : GOTO 2590
2690 IF K$ = DLT$ THEN GOTO 2390
2700 GOSUB 4710
2710 GOSUB 1590
2720 GOTO 2590
2730 REM->YEARDIGITTWO
2740 HTAB 27 : VTAB 9
2750 GET K$
2760 IF K$ < "0" OR "9" < K$ THEN GOTO 2820
2770 LET FIRST$ = LEFT$(YEAR$, 1)
2780 LET TEST$ = FIRST$ + K$
2790 LET TV = VAL(TEST$)
2800 IF 1 <= TV AND TV <= 99 THEN PRINT K$; : YEAR$ = TEST$ : YEAR = TV : GOTO 2940
2810 REM->YEARDIGITTWOKEYS
2820 IF K$ = " "             THEN GOTO 2940
2830 IF K$ = TB$        THEN GOTO 2940
2840 IF K$ = RT$ THEN GOTO 2940
2850 IF K$ = LT$  THEN GOTO 2590
2860 IF K$ = DOWN$ THEN GOTO 3640
2870 IF K$ = "+"             THEN GOSUB 4210 : PRINT LT$;YEAR$; : GOTO 2740
2880 IF K$ = "-"             THEN GOSUB 4280 : PRINT LT$;YEAR$; : GOTO 2740
2890 IF K$ = DLT$ THEN GOTO 2590
2900 GOSUB 4710
2910 GOSUB 1590
2920 GOTO 2740
2930 REM->HOURDIGITONE
2940 HTAB 15 : VTAB 14
2950 GET K$
2960 IF "0" <= K$ AND K$ <= "1" THEN PRINT K$; : HOUR$ = K$ + RIGHT$(HOUR$, 1) : GOTO 3090
2970 IF K$ = " "             THEN GOTO 3090
2980 IF K$ = TB$        THEN GOTO 3290
2990 IF K$ = RT$ THEN GOTO 3090
3000 IF K$ = LT$  THEN GOTO 2740
3010 IF K$ = UP$    THEN GOTO 1630
3020 IF K$ = "+"             THEN GOSUB 4350 : PRINT HOUR$; : GOTO 2940
3030 IF K$ = "-"             THEN GOSUB 4420 : PRINT HOUR$; : GOTO 2940
3040 IF K$ = DLT$ THEN GOTO 2740
3050 GOSUB 4710
3060 GOSUB 1590
3070 GOTO 2940
3080 REM->HOURDIGITTWO
3090 HTAB 16 : VTAB 14
3100 GET K$
3110 IF K$ < "0" OR "9" < K$ THEN GOTO 3170
3120 LET FIRST$ = LEFT$(HOUR$, 1)
3130 LET TEST$ = FIRST$ + K$
3140 LET TV = VAL(TEST$)
3150 IF 1 <= TV AND TV <= 12 THEN PRINT K$; : HOUR$ = TEST$ : HOUR = TV : GOTO 3290
3160 REM->HOURDIGITTWOKEYS
3170 IF K$ = " "             THEN GOTO 3290
3180 IF K$ = TB$        THEN GOTO 3290
3190 IF K$ = RT$ THEN GOTO 3290
3200 IF K$ = LT$  THEN GOTO 2940
3210 IF K$ = UP$    THEN GOTO 1630
3220 IF K$ = "+"             THEN GOSUB 4350 : PRINT LT$;HOUR$; : GOTO 3090
3230 IF K$ = "-"             THEN GOSUB 4420 : PRINT LT$;HOUR$; : GOTO 3090
3240 IF K$ = DLT$ THEN GOTO 2940
3250 GOSUB 4710
3260 GOSUB 1590
3270 GOTO 3090
3280 REM->MINUTEDIGITONE
3290 HTAB 20 : VTAB 14
3300 GET K$
3310 IF "0" <= K$ AND K$ <= "5" THEN PRINT K$; : MINUTE$ = K$ + RIGHT$(MINUTE$, 1) : GOTO 3440
3320 IF K$ = " "             THEN GOTO 3440
3330 IF K$ = TB$        THEN GOTO 3640
3340 IF K$ = RT$ THEN GOTO 3440
3350 IF K$ = LT$  THEN GOTO 3090
3360 IF K$ = UP$    THEN GOTO 1980
3370 IF K$ = "+"             THEN GOSUB 4490 : PRINT MINUTE$; : GOTO 3290
3380 IF K$ = "-"             THEN GOSUB 4560 : PRINT MINUTE$; : GOTO 3290
3390 IF K$ = DLT$ THEN GOTO 3090
3400 GOSUB 4710
3410 GOSUB 1590
3420 GOTO 3290
3430 REM->MINUTEDIGITTWO
3440 HTAB 21 : VTAB 14
3450 GET K$
3460 IF K$ < "0" OR "9" < K$ THEN GOTO 3520
3470 LET FIRST$ = LEFT$(MINUTE$, 1)
3480 LET TEST$ = FIRST$ + K$
3490 LET TV = VAL(TEST$)
3500 IF 0 <= TV AND TV <= 59 THEN PRINT K$; : MINUTE$ = TEST$ : MINUTE = TV : GOTO 3640
3510 REM->MINUTEDIGITTWOKEYS
3520 IF K$ = " "             THEN GOTO 3640
3530 IF K$ = TB$        THEN GOTO 3640
3540 IF K$ = RT$ THEN GOTO 3640
3550 IF K$ = LT$  THEN GOTO 3290
3560 IF K$ = UP$    THEN GOTO 1980
3570 IF K$ = "+"             THEN GOSUB 4490 : PRINT LT$;MINUTE$; : GOTO 3440
3580 IF K$ = "-"             THEN GOSUB 4560 : PRINT LT$;MINUTE$; : GOTO 3440
3590 IF K$ = DLT$ THEN GOTO 3290
3600 GOSUB 4710
3610 GOSUB 1590
3620 GOTO 3440
3630 REM->MERIDIEM
3640 HTAB 24 : VTAB 14
3650 GET K$
3660 IF K$ = "A" OR K$ = "P" THEN PRINT K$; : AMPM$ = K$ : GOTO 1630
3670 IF K$ = " "             THEN GOTO 1630
3680 IF K$ = TB$        THEN GOTO 1630
3690 IF K$ = RT$ THEN GOTO 1630
3700 IF K$ = LT$  THEN GOTO 3440
3710 IF K$ = UP$    THEN GOTO 2590
3720 IF K$ = "+"             THEN GOSUB 4630 : PRINT AMPM$; : GOTO 3640
3730 IF K$ = "-"             THEN GOSUB 4670 : PRINT AMPM$; : GOTO 3640
3740 IF K$ = DLT$ THEN GOTO 3440
3750 GOSUB 4710
3760 GOSUB 1590
3770 GOTO 3640
3780 REM->PRINTDATE
3790 GOSUB 3890
3800 PRINT YEAR$;"-";
3810 IF MNTH < 10 THEN PRINT "0";
3820 PRINT MNTH;"-";
3830 IF DAY < 10 THEN PRINT "0";
3840 PRINT DAY;" ";HOUR;":";
3850 IF MINUTE < 10 THEN PRINT "0";
3860 PRINT MINUTE;" ";AMPM$;"M"
3870 RETURN 
3880 REM->PADYEAR
3890 IF YEAR = 0 THEN YEAR$ = "0000": RETURN 
3900 LET YEAR$ = STR$(2000 + YEAR)
3910 RETURN 
3920 REM->INCDAY
3930 LET DAY = VAL(DAY$)
3940 LET DAY = DAY + 1
3950 GOSUB 1130
3960 IF DAY > MDAYS(MNTH) THEN DAY = 1
3970 LET DAY$ = STR$(DAY)
3980 IF DAY < 10 THEN DAY$ = "0" + DAY$
3990 RETURN
4000 REM->DECDAY
4010 LET DAY = VAL(DAY$)
4020 LET DAY = DAY - 1
4030 GOSUB 1130
4040 IF DAY <= 0 THEN DAY = MDAYS(MNTH)
4050 LET DAY$ = STR$(DAY)
4060 IF DAY < 10 THEN DAY$ = "0" + DAY$
4070 RETURN
4080 REM->INCMONTH
4090 GOSUB 1130
4100 LET MNTH = MNTH - 1
4110 IF MNTH < 1 THEN MNTH = 12
4120 GOSUB 1020
4130 RETURN
4140 REM->DECMONTH
4150 GOSUB 1130
4160 LET MNTH = MNTH + 1
4170 IF MNTH > 12 THEN MNTH = 1
4180 GOSUB 1020
4190 RETURN
4200 REM->INCYEAR
4210 LET YEAR = VAL(YEAR$)
4220 LET YEAR = YEAR + 1
4230 IF YEAR > 99 THEN YEAR = 0
4240 LET YEAR$ = STR$(YEAR)
4250 IF YEAR < 10 THEN YEAR$ = "0" + YEAR$
4260 RETURN
4270 REM->DECYEAR
4280 LET YEAR = VAL(YEAR$)
4290 LET YEAR = YEAR - 1
4300 IF YEAR < 0  THEN YEAR = 99
4310 LET YEAR$ = STR$(YEAR)
4320 IF YEAR < 10 THEN YEAR$ = "0" + YEAR$
4330 RETURN
4340 REM->INCHOUR
4350 LET HOUR = VAL(HOUR$)
4360 LET HOUR = HOUR + 1
4370 IF HOUR > 12 THEN HOUR = 1
4380 LET HOUR$ = STR$(HOUR)
4390 IF HOUR < 10 THEN HOUR$ = "0" + HOUR$
4400 RETURN
4410 REM->DECHOUR
4420 LET HOUR = VAL(HOUR$)
4430 LET HOUR = HOUR - 1
4440 IF HOUR < 1 THEN HOUR = 12
4450 LET HOUR$ = STR$(HOUR)
4460 IF HOUR < 10 THEN HOUR$ = "0" + HOUR$
4470 RETURN
4480 REM->INCMINUTE
4490 LET MINUTE = VAL(MINUTE$)
4500 LET MINUTE = MINUTE + 1
4510 IF MINUTE > 59 THEN MINUTE = 0
4520 LET MINUTE$ = STR$(MINUTE)
4530 IF MINUTE < 10 THEN MINUTE$ = "0" + MINUTE$
4540 RETURN
4550 REM->DECMINUTE
4560 LET MINUTE = VAL(MINUTE$)
4570 LET MINUTE = MINUTE - 1
4580 IF MINUTE < 0 THEN MINUTE = 59
4590 LET MINUTE$ = STR$(MINUTE)
4600 IF MINUTE < 10 THEN MINUTE$ = "0" + MINUTE$
4610 RETURN
4620 REM->INCMERIDIEM
4630 IF AMPM$ = "A" THEN AMPM$ = "P" : RETURN
4640 IF AMPM$ = "P" THEN AMPM$ = "A"
4650 RETURN
4660 REM->DECMERIDIEM
4670 IF AMPM$ = "A" THEN AMPM$ = "P" : RETURN
4680 IF AMPM$ = "P" THEN AMPM$ = "A"
4690 RETURN
4700 REM->JUMPKEYS
4710 IF K$ = ESC$ THEN POP : RETURN
4720 IF K$ = RET$ THEN POP : RETURN
4730 IF PEEK(49249) <= 127 THEN RETURN
4740 IF K$ = "D" THEN POP : GOTO 1630
4750 IF K$ = "M" THEN POP : GOTO 1980
4760 IF K$ = "Y" THEN POP : GOTO 2590
4770 IF K$ = "H" THEN POP : GOTO 2940
4780 IF K$ = "N" THEN POP : GOTO 3290
4790 IF K$ = "R" THEN POP : GOTO 3640
4800 RETURN
RUN
