10 REM SETDATEVALUES.BAS - 17/02/2019 - 06h53
20 REM ***********************
30 REM PROGRAM: SETDATE
40 REM SYSTEM:  PRODOS
50 REM AUTHOR:  BILL CHATFIELD
60 REM LICENSE: GPL3
70 REM ***********************
80 DEF FN ODD(X) = (X / 2 -  INT (X / 2)) > 0
90 GOSUB 1000:REM GO->GETDATE 
100 IF YEAR <> 0 AND MNTH <> 0 AND DAY <> 0 THEN GOTO 140:REM GO->PRINTINITIALDATE 
110 PRINT "PLEASE ENTER THE DATE AND TIME."
120 GOTO 230:REM GO->ENTERYEAR 
130 REM->PRINTINITIALDATE
140 PRINT "SYSTEM DATE: ";
150 GOSUB 900:REM GO->PRINTDATE 
160 REM->QUESTION
170 PRINT "DO YOU WANT TO CHANGE IT? (Y/N) ";
180 GET K$
190 PRINT K$
200 IF K$ =  "N" THEN END 
210 IF K$ <> "Y" THEN GOTO 170:REM GO->QUESTION 
220 REM->ENTERYEAR
230 PRINT "YEAR (1-99) [RET FOR ";YEAR;"] ";
240 INPUT "";YEAR$
250 IF YEAR$ < > "" THEN YEAR = VAL(YEAR$)
260 IF 1 <= YEAR AND YEAR <= 99 THEN GOTO 330:REM GO->ENTERMONTH 
270 INVERSE
280 PRINT "INVALID INPUT:";
290 NORMAL
300 PRINT " YEAR ";YEAR;" NOT IN RANGE."
310 GOTO 230:REM GO->ENTERYEAR 
320 REM->ENTERMONTH
330 PRINT "MONTH (1-12) [RET FOR ";MNTH;"] ";
340 INPUT "";MNTH$
350 IF MNTH$ <> "" THEN MNTH = VAL(MNTH$)
360 IF 1 <= MNTH AND MNTH <= 12 THEN GOTO 430:REM GO->ENTERDAY 
370 INVERSE
380 PRINT "INVALID INPUT:";
390 NORMAL
400 PRINT " MONTH ";MNTH;" NOT IN RANGE."
410 GOTO 330:REM GO->ENTERMONTH 
420 REM->ENTERDAY
430 PRINT "DAY (1-31) [RET FOR ";DAY;"] ";
440 INPUT "";DAY$
450 IF DAY$ < > "" THEN DAY = VAL(DAY$)
460 IF 1 <= DAY AND DAY <= 31 THEN GOTO 530:REM GO->ENTERHOUR 
470 INVERSE
480 PRINT "INVALID INPUT:";
490 NORMAL
500 PRINT " DAY ";DAY;" NOT IN RANGE."
510 GOTO 430:REM GO->ENTERDAY 
520 REM->ENTERHOUR
530 PRINT "HOUR (1-12) [RET FOR ";HOUR;"] ";
540 INPUT "";HOUR$
550 IF HOUR$ < > "" THEN HOUR = VAL(HOUR$)
560 IF 1 <= HOUR AND HOUR <= 12 THEN GOTO 630:REM GO->ENTERMINUTE 
570 INVERSE
580 PRINT "INVALID INPUT:";
590 NORMAL
600 PRINT " HOUR ";HOUR;" NOT IN RANGE."
610 GOTO 530:REM GO->ENTERHOUR 
620 REM->ENTERMINUTE
630 PRINT "MINUTE (0-59) [RET FOR ";MINUTE;"] ";
640 INPUT "";MINUTE$
650 IF MINUTE$ < > "" THEN MINUTE = VAL(MINUTE$)
660 IF 0 <= MINUTE AND MINUTE <= 59 THEN GOTO 730:REM GO->ENTERAMPM 
670 INVERSE
680 PRINT "INVALID INPUT:";
690 NORMAL
700 PRINT " MINUTE ";MINUTE;" NOT IN RANGE."
710 GOTO 630:REM GO->ENTERMINUTE 
720 REM->ENTERAMPM
730 PRINT "AM/PM (A OR P) [RET FOR ";AMPM$;"M] ";
740 INPUT "";M$
750 IF M$ < > "" THEN AMPM$ = M$
760 IF AMPM$ = "A" OR AMPM$ = "P" OR AMPM$ = "AM" OR AMPM$ = "PM" THEN GOTO 830:REM GO->CONTAMPM 
770 INVERSE
780 PRINT "INVALID INPUT:";
790 NORMAL
800 PRINT " MUST BE A OR P."
810 GOTO 730:REM GO->ENTERAMPM 
820 REM->CONTAMPM
830 LET AMPM$ = LEFT$(AMPM$,1)
840 GOSUB 1170:REM GO->SETDATE 
850 PRINT
860 PRINT "SET SYSTEM DATE: ";
870 GOSUB 900:REM GO->PRINTDATE 
880 END 
890 REM->PRINTDATE
900 GOSUB 1320:REM GO->PADYEAR 
910 PRINT YEAR$;"-";
920 IF MNTH < 10 THEN PRINT "0";
930 PRINT MNTH;"-";
940 IF DAY < 10 THEN PRINT "0";
950 PRINT DAY;" ";HOUR;":";
960 IF MINUTE < 10 THEN PRINT "0";
970 PRINT MINUTE;" ";AMPM$;"M"
980 RETURN 
990 REM->GETDATE
1000 LET DH = PEEK(49041): REM DATE HIGH BYTE
1010 LET DL = PEEK(49040): REM DATE LOW BYTE
1020 LET TH = PEEK(49043): REM TIME HIGH BYTE
1030 LET TL = PEEK(49042): REM TIME LOW BYTE 
1040 LET YEAR = INT (DH / 2)
1050 LET MNTH = FN ODD(DH) * 2 ^ 3 +  INT (DL / 32)
1060 LET DAY = DL
1070 IF DAY >= 128 THEN DAY = DAY - 128
1080 IF DAY >= 64  THEN DAY = DAY - 64
1090 IF DAY >= 32  THEN DAY = DAY - 32
1100 IF TH = 0  THEN HOUR = 12      : AMPM$ = "A"
1110 IF TH = 12 THEN HOUR = 12      : AMPM$ = "P"
1120 IF TH > 12 THEN HOUR = TH - 12 : AMPM$ = "P"
1130 IF 0 < TH AND TH < 12 THEN HOUR = TH      : AMPM$ = "A"
1140 LET MINUTE = TL
1150 RETURN 
1160 REM->SETDATE
1170 IF YEAR >= 100 THEN Y2 = VAL(RIGHT$(STR$(YEAR),2))
1180 IF YEAR <  100 THEN Y2 = YEAR
1190 LET DH = Y2 * 2 + (MNTH >  = 8)
1200 LET DL = MNTH * 32 - 128 * (MNTH >  = 8) + DAY
1210 IF HOUR =  12 AND AMPM$ = "A" THEN TH = 0
1220 IF HOUR =  12 AND AMPM$ = "P" THEN TH = 12
1230 IF HOUR <> 12 AND AMPM$ = "P" THEN TH = HOUR + 12
1240 IF HOUR <> 12 AND AMPM$ = "A" THEN TH = HOUR
1250 LET TL = MINUTE
1260 POKE 49041,DH: REM DATE HIGH BYTE
1270 POKE 49040,DL: REM DATE LOW BYTE
1280 POKE 49043,TH: REM TIME HIGH BYTE
1290 POKE 49042,TL: REM TIME LOW BYTE
1300 RETURN 
1310 REM->PADYEAR
1320 IF YEAR = 0 THEN YEAR$ = "0000": RETURN 
1330 LET YEAR$ = STR$(2000 + YEAR)
1340 RETURN 