 10  REM ***********************
 15  REM PROGRAM: SETDATE
 17  REM SYSTEM:  PRODOS
 20  REM AUTHOR:  BILL CHATFIELD
 25  REM LICENSE: GPL3
 30  REM ***********************
 40  DEF  FN ODD(X) = (X / 2 -  INT (X / 2)) > 0
 50  GOSUB 1000: REM GET THE DATE
 53  IF YEAR = 0 OR MNTH = 0 OR DAY = 0 THEN  GOTO 90
 55  PRINT "SYSTEM DATE: ";
 60  GOSUB 160: REM PRINT DATE
 70  PRINT "DO YOU WANT TO CHANGE IT? (Y/N) ";
 75  GET K$: PRINT K$
 76  IF K$ = "N" THEN  END 
 77  IF K$ <  > "Y" THEN  GOTO 70
 80  GOTO 100
 90  PRINT "PLEASE ENTER THE DATE AND TIME."
 100  PRINT "YEAR (1-99) [RET FOR ";YEAR;"] ";: INPUT "";YEAR$
 103  IF YEAR$ <  > "" THEN YEAR =  VAL (YEAR$)
 105  IF YEAR < 1 OR 99 < YEAR THEN  INVERSE : PRINT "INVALID INPUT:";: NORMAL : PRINT " YEAR ";YEAR;" NOT IN RANGE.": GOTO 100
 110  PRINT "MONTH (1-12) [RET FOR ";MNTH;"] ";: INPUT "";MNTH$
 113  IF MNTH$ <  > "" THEN MNTH =  VAL (MNTH$)
 115  IF MNTH < 1 OR 12 < MNTH THEN  INVERSE : PRINT "INVALID INPUT:";: NORMAL : PRINT " MONTH ";MNTH;" NOT IN RANGE.": GOTO 110
 120  PRINT "DAY (1-31) [RET FOR ";DAY;"] ";: INPUT "";DAY$
 123  IF DAY$ <  > "" THEN DAY =  VAL (DAY$)
 125  IF DAY < 1 OR 31 < DAY THEN  INVERSE : PRINT "INVALID INPUT:";: NORMAL : PRINT " DAY ";DAY;" NOT IN RANGE.": GOTO 120
 130  PRINT "HOUR (1-12) [RET FOR ";HOUR;"] ";: INPUT "";HOUR$
 133  IF HOUR$ <  > "" THEN HOUR =  VAL (HOUR$)
 135  IF HOUR < 1 OR 13 < HOUR THEN  INVERSE : PRINT "INVALID INPUT:";: NORMAL : PRINT " HOUR ";HOUR;" NOT IN RANGE.": GOTO 130
 140  PRINT "MINUTE (0-59) [RET FOR ";MINUTE;"] ";: INPUT "";MINUTE$
 143  IF MINUTE$ <  > "" THEN MINUTE =  VAL (MINUTE$)
 145  IF MINUTE < 0 OR 59 < MINUTE THEN  INVERSE : PRINT "INVALID INPUT:";: NORMAL : PRINT " MINUTE ";MINUTE;" NOT IN RANGE.": GOTO 140
 150  PRINT "AM/PM (A OR P) [RET FOR ";AMPM$;"M] ";: INPUT "";M$
 152  IF AMPM$ <  > "A" AND AMPM$ <  > "P" AND AMPM$ <  > "AM" AND AMPM$ < > "PM" THEN  INVERSE : PRINT "INVALID INPUT:";: NORMAL : PRINT " MUST BE A OR P.": GOTO 150
 153  IF M$ <  > "" THEN AMPM$ = M$
 154  LET AMPM$ =  LEFT$ (AMPM$,1)
 155  GOSUB 2000: REM SET DATE
 156  PRINT : PRINT "SET SYSTEM DATE: ";: GOSUB 160: END 
 157  REM **********************
 158  REM SUBROUTINE: PRINT DATE
 159  REM **********************
 160  GOSUB 3000: REM PAD YEAR
 161  PRINT YEAR$;"-";
 162  IF MNTH < 10 THEN  PRINT "0";
 164  PRINT MNTH;"-";
 166  IF DAY < 10 THEN  PRINT "0";
 168  PRINT DAY;" ";HOUR;":";
 170  IF MINUTE < 10 THEN  PRINT "0";
 180  PRINT MINUTE;" ";AMPM$;"M"
 190  RETURN 
 1000  REM ********************
 1010  REM SUBROUTINE: GET DATE
 1020  REM ********************
 1022  LET DH =  PEEK (49041): REM DATE HIGH BYTE
 1023  LET DL =  PEEK (49040): REM DATE LOW BYTE
 1024  LET TH =  PEEK (49043): REM TIME HIGH BYTE
 1025  LET TL =  PEEK (49042): REM TIME LOW BYTE 
 1030  LET YEAR =  INT (DH / 2)
 1040  LET MNTH =  FN ODD(DH) * 2 ^ 3 +  INT (DL / 32)
 1050  LET DAY = DL
 1052  IF DAY >  = 128 THEN DAY = DAY - 128
 1054  IF DAY >  = 64 THEN DAY = DAY - 64
 1056  IF DAY >  = 32 THEN DAY = DAY - 32
 1060  IF TH = 0 THEN HOUR = 12:AMPM$ = "A": GOTO 1070
 1065  IF TH = 12 THEN HOUR = 12:AMPM$ = "P": GOTO 1070
 1067  IF TH > 12 THEN HOUR = TH - 12:AMPM$ = "P": GOTO 1070
 1068  LET HOUR = TH
 1069  LET AMPM$ = "A"
 1070  LET MINUTE = TL
 1080  RETURN 
 2000  REM ********************
 2010  REM SUBROUTINE: SET DATE
 2020  REM ********************
 2021  REM CONVERT TO 2-DIGIT YEAR WITHOUT MODIFY ORIG
 2025  IF YEAR >  = 100 THEN Y2 =  VAL ( RIGHT$ ( STR$ (YEAR),2)): GOTO 2030
 2027  LET Y2 = YEAR
 2030  LET DH = Y2 * 2 + (MNTH >  = 8)
 2040  LET DL = MNTH * 32 - 128 * (MNTH >  = 8) + DAY
 2053  IF HOUR = 12 AND AMPM$ = "A" THEN TH = 0: GOTO 2060
 2055  IF HOUR = 12 AND AMPM$ = "P" THEN TH = 12: GOTO 2060
 2057  IF AMPM$ = "P" THEN TH = HOUR + 12: GOTO 2060
 2058  LET TH = HOUR
 2060  LET TL = MINUTE
 2070  POKE 49041,DH: REM DATE HIGH BYTE
 2080  POKE 49040,DL: REM DATE LOW BYTE
 2090  POKE 49043,TH: REM TIME HIGH BYTE
 2100  POKE 49042,TL: REM TIME LOW BYTE
 2110  RETURN 
 3000  REM ********************
 3010  REM SUBROUTINE: PAD YEAR
 3020  REM ********************
 3030  IF YEAR = 0 THEN YEAR$ = "0000": RETURN 
 3040  LET YEAR$ =  STR$ (2000 + YEAR)
 3080  RETURN 
 
