NEW
10 REM  - /HOME/BILL/GIT/SETDATE/SETDATE.BAS - 07/02/2019 - 06h11
20 REM ***********************
30 REM PROGRAM: DATEENTRYFORM
40 REM SYSTEM:  PRODOS
50 REM AUTHOR:  BILL CHATFIELD
60 REM LICENSE: GPL3
70 REM ***********************
80 LET D$=CHR$(4)
90 LET LT$=CHR$(8)
100 LET DOWN$=CHR$(10)
110 LET UP$=CHR$(11)
120 LET RET$=CHR$(13)
130 LET D1$=CHR$(17) 
140 LET D2$=CHR$(18) 
150 LET NAK$=CHR$(21)   
160 LET RT$=CHR$(21)
170 LET ESC$=CHR$(27)
180 LET DLT$=CHR$(127)
190 LET BEEP$=CHR$(7)
200 LET TB$=CHR$(9)
210 DIM MNTHS$(12), MDAYS(12)
220 FOR I = 1 TO 12
230 READ MNTHS$(I), MDAYS(I)
240 NEXT
250 DATA JAN,31
260 DATA FEB,29
270 DATA MAR,31
280 DATA APR,30
290 DATA MAY,31
300 DATA JUN,30
310 DATA JUL,31
320 DATA AUG,31
330 DATA SEP,30
340 DATA OCT,31
350 DATA NOV,30
360 DATA DEC,31
370 GOSUB 1020
380 GOSUB 770
390 GOSUB 480
400 GOSUB 1370
410 GOSUB 960
420 GOSUB 1190
430 VTAB TY : POKE 36,38 : PRINT
440 IF K$ = RET$ THEN PRINT "SUCCESSFULLY SET SYSTEM DATE AND TIME."
450 IF K$ <>RET$ THEN PRINT "CANCELLED SETTING OF DATE AND TIME."
460 END
470 REM->DRAWSCREEN
480 PRINT "DATE: ";DAY$;"-";MNTH$;"-";YEAR$;     "  (DD-MMM-YY)"
490 PRINT "TIME: ";HOUR$;":";MINUTE$;" ";AMPM$;"M"
500 LET TX = 7 : REM TIME HORIZONTAL POSITION
510 LET DX = 7 : REM DATE HORIZONTAL POSITION
520 CALL -998 : REM MOVE CURSOR UP 1 LINE TO TIME LINE
530 LET TY = PEEK(37) + 1 : REM TIME VERTICAL POSITION
540 CALL -998 : REM MOVE CURSOR UP 1 LINE TO DATE LINE
550 LET DY = PEEK(37) + 1 : REM DATE VERTICAL POSITION
560 RETURN
570 REM->DISPLAYHELP
580 VTAB TY : POKE 36,38 : PRINT
590 PRINT
600 PRINT "HELP"
610 PRINT "RETURN - SET DATA & TIME TO ENTERED VALUES"
620 PRINT "ESC    - EXIT WITHOUT MAKING ANY CHAGES"
630 PRINT "TAB    - SKIP TO NEXT ELEMENT"
640 PRINT "ARROWS - MOVE ONE CHARACTER IN ANY DIRECTION"
650 PRINT "SPACE  - MOVE FORWARD ONE CHARACTER"
660 PRINT "DELETE - MOVE BACK ONE CHARACTER"
670 PRINT "+      - INCREMENT CURRENT VALUE"
680 PRINT "-      - DECREMENT CURRENT VALUE"
690 PRINT "?      - DISPLAY HELP"
700 PRINT
710 GOSUB 480
720 RETURN
730 REM->FORMATMONTH
740 LET MNTH$ = MNTHS$(MNTH)
750 RETURN
760 REM->FORMATDATEANDTIME
770 LET YEAR$ = STR$(YEAR)
780 IF LEN(YEAR$) = 1 THEN LET YEAR$ = "0" + YEAR$
790 GOSUB 740
800 LET DAY$ = STR$(DAY)
810 IF LEN(DAY$) = 1 THEN LET DAY$ = "0" + DAY$
820 LET HOUR$ = STR$(HOUR)
830 IF LEN(HOUR$) = 1 THEN LET HOUR$ = "0" + HOUR$
840 LET MINUTE$ = STR$(MINUTE)
850 IF LEN(MINUTE$) = 1 THEN LET MINUTE$ = "0" + MINUTE$
860 RETURN
870 REM->DECODEMONTH
880 FOR I = 1 TO 12
890 IF MNTHS$(I) = MNTH$ THEN MNTH = I : RETURN
900 NEXT
910 VTAB 24
920 PRINT
930 PRINT "INVALID MONTH NAME: " + MNTH$
940 END
950 REM->DECODEDATEANDTIME
960 LET YEAR = VAL(YEAR$)
970 GOSUB 880
980 LET HOUR = VAL(HOUR$)
990 LET MINUTE = VAL(MINUTE$)
1000 RETURN
1010 REM->GETSYSTEMDATE
1020 LET DH = PEEK(49041): REM DATE HIGH BYTE
1030 LET DL = PEEK(49040): REM DATE LOW BYTE
1040 LET TH = PEEK(49043): REM TIME HIGH BYTE
1050 LET TL = PEEK(49042): REM TIME LOW BYTE 
1060 LET YEAR = INT (DH / 2)
1070 LET MNTH = ((DH/2 - INT(DH/2)) > 0) * 2 ^ 3 +  INT (DL / 32)
1080 LET DAY = DL
1090 IF DAY >= 128 THEN DAY = DAY - 128
1100 IF DAY >= 64  THEN DAY = DAY - 64
1110 IF DAY >= 32  THEN DAY = DAY - 32
1120 IF TH = 0  THEN HOUR = 12      : AMPM$ = "A"
1130 IF TH = 12 THEN HOUR = 12      : AMPM$ = "P"
1140 IF TH > 12 THEN HOUR = TH - 12 : AMPM$ = "P"
1150 IF TH < 12 THEN HOUR = TH      : AMPM$ = "A"
1160 LET MINUTE = TL
1170 RETURN 
1180 REM->SETSYSTEMDATEANDTIME
1190 IF YEAR >= 100 THEN Y2 = VAL(RIGHT$(STR$(YEAR),2))
1200 IF YEAR <  100 THEN Y2 = YEAR
1210 LET DH = Y2 * 2 + (MNTH >  = 8)
1220 LET DL = MNTH * 32 - 128 * (MNTH >  = 8) + DAY
1230 IF HOUR =  12 AND AMPM$ = "A" THEN TH = 0
1240 IF HOUR =  12 AND AMPM$ = "P" THEN TH = 12
1250 IF HOUR <> 12 AND AMPM$ = "P" THEN TH = HOUR + 12
1260 IF HOUR <> 12 AND AMPM$ = "A" THEN TH = HOUR
1270 LET TL = MINUTE
1280 POKE 49041,DH: REM DATE HIGH BYTE
1290 POKE 49040,DL: REM DATE LOW BYTE
1300 POKE 49043,TH: REM TIME HIGH BYTE
1310 POKE 49042,TL: REM TIME LOW BYTE
1320 RETURN 
1330 REM->SOUNDERROR
1340 PRINT BEEP$
1350 RETURN
1360 REM->ENTERDATEANDTIME
1370 REM->DAYDIGITONE
1380 HTAB DX : VTAB DY
1390 GET K$
1400 IF "0" <= K$ AND K$ <= "3" THEN PRINT K$; : DAY$ = K$ + RIGHT$(DAY$, 1) : GOTO 1550
1410 IF K$ = " "             THEN GOTO 1550
1420 IF K$ = TB$        THEN GOTO 1770
1430 IF K$ = RT$ THEN GOTO 1550
1440 IF K$ = LT$  THEN GOTO 3610
1450 IF K$ = DOWN$ THEN GOTO 2830
1460 IF K$ = "+"             THEN GOSUB 3820 : PRINT DAY$; : GOTO 1380
1470 IF K$ = "-"             THEN GOSUB 3900 : PRINT DAY$; : GOTO 1380
1480 IF K$ = DLT$ THEN GOTO 3610
1490 IF K$ = ESC$ THEN RETURN
1500 IF K$ = RET$ THEN RETURN
1510 IF K$ = "/" OR K$ = "?" THEN GOSUB 580 : GOTO 1380
1520 GOSUB 1340
1530 GOTO 1380
1540 REM->DAYDIGITTWO
1550 HTAB DX+1 : VTAB DY
1560 GET K$
1570 IF K$ < "0" OR "9" < K$ THEN GOTO 1630
1580 LET FIRST$ = LEFT$(DAY$, 1)
1590 LET TEST$ = FIRST$ + K$
1600 LET TV = VAL(TEST$)
1610 IF 1 <= TV AND TV <= MDAYS(MNTH) THEN PRINT K$; : DAY$ = TEST$ : DAY = TV : GOTO 1770
1620 REM->DAYDIGITTWOKEYS
1630 IF K$ = " "             THEN GOTO 1770
1640 IF K$ = TB$        THEN GOTO 1770
1650 IF K$ = RT$ THEN GOTO 1770
1660 IF K$ = LT$  THEN GOTO 1380
1670 IF K$ = DOWN$ THEN GOTO 3000
1680 IF K$ = "+"             THEN GOSUB 3820 : PRINT LT$;DAY$; : GOTO 1550
1690 IF K$ = "-"             THEN GOSUB 3900 : PRINT LT$;DAY$; : GOTO 1550
1700 IF K$ = DLT$ THEN GOTO 1380
1710 IF K$ = ESC$ THEN RETURN
1720 IF K$ = RET$ THEN RETURN
1730 IF K$ = "/" OR K$ = "?" THEN GOSUB 580 : GOTO 1550
1740 GOSUB 1340
1750 GOTO 1550
1760 REM->MONTHCHARONE
1770 HTAB DX+3 : VTAB DY
1780 GET K$
1790 IF K$ = LEFT$(MNTH$, 1) THEN GOTO 2020
1800 IF K$ = "J" THEN MNTH$ = "JAN" : MNTH=1  : PRINT MNTH$; : GOTO 2020
1810 IF K$ = "F" THEN MNTH$ = "FEB" : MNTH=2  : PRINT MNTH$; : GOTO 2020
1820 IF K$ = "M" THEN MNTH$ = "MAR" : MNTH=3  : PRINT MNTH$; : GOTO 2020
1830 IF K$ = "A" THEN MNTH$ = "APR" : MNTH=4  : PRINT MNTH$; : GOTO 2020
1840 IF K$ = "S" THEN MNTH$ = "SEP" : MNTH=9  : PRINT MNTH$; : GOTO 2020
1850 IF K$ = "O" THEN MNTH$ = "OCT" : MNTH=10 : PRINT MNTH$; : GOTO 2020
1860 IF K$ = "N" THEN MNTH$ = "NOV" : MNTH=11 : PRINT MNTH$; : GOTO 2020
1870 IF K$ = "D" THEN MNTH$ = "DEC" : MNTH=12 : PRINT MNTH$; : GOTO 2020
1880 IF K$ = " "             THEN GOTO 2020
1890 IF K$ = TB$        THEN GOTO 2440
1900 IF K$ = RT$ THEN GOTO 2020
1910 IF K$ = LT$  THEN GOTO 1550
1920 IF K$ = DOWN$ THEN GOSUB 3220
1930 IF K$ = "+"             THEN GOSUB 3980 : PRINT MNTH$; : GOTO 1770
1940 IF K$ = "-"             THEN GOSUB 4040 : PRINT MNTH$; : GOTO 1770
1950 IF K$ = DLT$ THEN GOTO 1550
1960 IF K$ = ESC$ THEN RETURN
1970 IF K$ = RET$ THEN RETURN
1980 IF K$ = "/" OR K$ = "?" THEN GOSUB 580 : GOTO 1770
1990 GOSUB 1340
2000 GOTO 1770
2010 REM->MONTHCHARTWO
2020 HTAB DX+4 : VTAB DY
2030 GET K$
2040 IF K$ = MID$(MNTH$, 2, 1) THEN GOTO 2220
2050 LET FIRST$ = LEFT$(MNTH$, 1)
2060 IF FIRST$ = "A" AND K$ = "P" THEN MNTH$ = "APR" : MNTH=4 : PRINT "PR"; : GOTO 2220
2070 IF FIRST$ = "A" AND K$ = "U" THEN MNTH$ = "AUG" : MNTH=8 : PRINT "UG"; : GOTO 2220
2080 IF K$ = " "             THEN GOTO 2220
2090 IF K$ = TB$        THEN GOTO 2440
2100 IF K$ = RT$ THEN GOTO 2220
2110 IF K$ = LT$  THEN GOTO 1770
2120 IF K$ = DOWN$ THEN GOTO 3390
2130 IF K$ = "+"             THEN GOSUB 3980 : PRINT LT$;MNTH$; : GOTO 2020
2140 IF K$ = "-"             THEN GOSUB 4040 : PRINT LT$;MNTH$; : GOTO 2020
2150 IF K$ = DLT$ THEN GOTO 1770
2160 IF K$ = ESC$ THEN RETURN
2170 IF K$ = RET$ THEN RETURN
2180 IF K$ = "/" OR K$ = "?" THEN GOSUB 580 : GOTO 2020
2190 GOSUB 1340
2200 GOTO 2020
2210 REM->MONTHCHARTHREE
2220 HTAB DX+5 : VTAB DY
2230 IF K$ = RIGHT$(MNTH$, 1) THEN GOTO 2440
2240 LET TWO$ = LEFT$(MNTH$, 2)
2250 GET K$
2260 IF TWO$ = "MA" AND K$ = "R" THEN MNTH$ = "MAR" : MNTH=3 : PRINT K$; : GOTO 2440
2270 IF TWO$ = "MA" AND K$ = "Y" THEN MNTH$ = "MAY" : MNTH=5 : PRINT K$; : GOTO 2440
2280 IF TWO$ = "JU" AND K$ = "N" THEN MNTH$ = "JUN" : MNTH=6 : PRINT K$; : GOTO 2440
2290 IF TWO$ = "JU" AND K$ = "L" THEN MNTH$ = "JUL" : MNTH=7 : PRINT K$; : GOTO 2440
2300 IF K$ = " "             THEN GOTO 2440
2310 IF K$ = TB$        THEN GOTO 2440
2320 IF K$ = RT$ THEN GOTO 2440
2330 IF K$ = LT$  THEN GOTO 2020
2340 IF K$ = DOWN$ THEN GOTO 3610
2350 IF K$ = "+"             THEN GOSUB 3980 : PRINT LT$;LT$;MNTH$; : GOTO 2220
2360 IF K$ = "-"             THEN GOSUB 4040 : PRINT LT$;LT$;MNTH$; : GOTO 2220
2370 IF K$ = DLT$ THEN GOTO 2020
2380 IF K$ = ESC$ THEN RETURN
2390 IF K$ = RET$ THEN RETURN
2400 IF K$ = "/" OR K$ = "?" THEN GOSUB 580 : GOTO 2220
2410 GOSUB 1340
2420 GOTO 2220
2430 REM->YEARDIGITONE
2440 HTAB DX+7 : VTAB DY
2450 GET K$
2460 IF "0" <= K$ AND K$ <= "9" THEN PRINT K$; : YEAR$ = K$ + RIGHT$(YEAR$, 1) : GOTO 2610
2470 IF K$ = " "             THEN GOTO 2610
2480 IF K$ = TB$        THEN GOTO 2830
2490 IF K$ = RT$ THEN GOTO 2610
2500 IF K$ = LT$  THEN GOTO 2220
2510 IF K$ = DOWN$ THEN GOTO 3610
2520 IF K$ = "+"             THEN GOSUB 4100 : PRINT YEAR$; : GOTO 2440
2530 IF K$ = "-"             THEN GOSUB 4170 : PRINT YEAR$; : GOTO 2440
2540 IF K$ = DLT$ THEN GOTO 2220
2550 IF K$ = ESC$ THEN RETURN
2560 IF K$ = RET$ THEN RETURN
2570 IF K$ = "/" OR K$ = "?" THEN GOSUB 580 : GOTO 2440
2580 GOSUB 1340
2590 GOTO 2440
2600 REM->YEARDIGITTWO
2610 HTAB DX+8 : VTAB DY
2620 GET K$
2630 IF K$ < "0" OR "9" < K$ THEN GOTO 2690
2640 LET FIRST$ = LEFT$(YEAR$, 1)
2650 LET TEST$ = FIRST$ + K$
2660 LET TV = VAL(TEST$)
2670 IF 1 <= TV AND TV <= 99 THEN PRINT K$; : YEAR$ = TEST$ : YEAR = TV : GOTO 2830
2680 REM->YEARDIGITTWOKEYS
2690 IF K$ = " "             THEN GOTO 2830
2700 IF K$ = TB$        THEN GOTO 2830
2710 IF K$ = RT$ THEN GOTO 2830
2720 IF K$ = LT$  THEN GOTO 2440
2730 IF K$ = DOWN$ THEN GOTO 3610
2740 IF K$ = "+"             THEN GOSUB 4100 : PRINT LT$;YEAR$; : GOTO 2610
2750 IF K$ = "-"             THEN GOSUB 4170 : PRINT LT$;YEAR$; : GOTO 2610
2760 IF K$ = DLT$ THEN GOTO 2440
2770 IF K$ = ESC$ THEN RETURN
2780 IF K$ = RET$ THEN RETURN
2790 IF K$ = "/" OR K$ = "?" THEN GOSUB 580 : GOTO 2610
2800 GOSUB 1340
2810 GOTO 2610
2820 REM->HOURDIGITONE
2830 HTAB TX : VTAB TY
2840 GET K$
2850 IF "0" <= K$ AND K$ <= "1" THEN PRINT K$; : HOUR$ = K$ + RIGHT$(HOUR$, 1) : GOTO 3000
2860 IF K$ = " "             THEN GOTO 3000
2870 IF K$ = TB$        THEN GOTO 3220
2880 IF K$ = RT$ THEN GOTO 3000
2890 IF K$ = LT$  THEN GOTO 2610
2900 IF K$ = UP$    THEN GOTO 1380
2910 IF K$ = "+"             THEN GOSUB 4240 : PRINT HOUR$; : GOTO 2830
2920 IF K$ = "-"             THEN GOSUB 4310 : PRINT HOUR$; : GOTO 2830
2930 IF K$ = DLT$ THEN GOTO 2610
2940 IF K$ = ESC$ THEN RETURN
2950 IF K$ = RET$ THEN RETURN
2960 IF K$ = "/" OR K$ = "?" THEN GOSUB 580 : GOTO 2830
2970 GOSUB 1340
2980 GOTO 2830
2990 REM->HOURDIGITTWO
3000 HTAB TX+1 : VTAB TY
3010 GET K$
3020 IF K$ < "0" OR "9" < K$ THEN GOTO 3080
3030 LET FIRST$ = LEFT$(HOUR$, 1)
3040 LET TEST$ = FIRST$ + K$
3050 LET TV = VAL(TEST$)
3060 IF 1 <= TV AND TV <= 12 THEN PRINT K$; : HOUR$ = TEST$ : HOUR = TV : GOTO 3220
3070 REM->HOURDIGITTWOKEYS
3080 IF K$ = " "             THEN GOTO 3220
3090 IF K$ = TB$        THEN GOTO 3220
3100 IF K$ = RT$ THEN GOTO 3220
3110 IF K$ = LT$  THEN GOTO 2830
3120 IF K$ = UP$    THEN GOTO 1550
3130 IF K$ = "+"             THEN GOSUB 4240 : PRINT LT$;HOUR$; : GOTO 3000
3140 IF K$ = "-"             THEN GOSUB 4310 : PRINT LT$;HOUR$; : GOTO 3000
3150 IF K$ = DLT$ THEN GOTO 2830
3160 IF K$ = ESC$ THEN RETURN
3170 IF K$ = RET$ THEN RETURN
3180 IF K$ = "/" OR K$ = "?" THEN GOSUB 580 : GOTO 3000
3190 GOSUB 1340
3200 GOTO 3000
3210 REM->MINUTEDIGITONE
3220 HTAB TX+3 : VTAB TY
3230 GET K$
3240 IF "0" <= K$ AND K$ <= "5" THEN PRINT K$; : MINUTE$ = K$ + RIGHT$(MINUTE$, 1) : GOTO 3390
3250 IF K$ = " "             THEN GOTO 3390
3260 IF K$ = TB$        THEN GOTO 3610
3270 IF K$ = RT$ THEN GOTO 3390
3280 IF K$ = LT$  THEN GOTO 3000
3290 IF K$ = UP$    THEN GOTO 1770
3300 IF K$ = "+"             THEN GOSUB 4380 : PRINT MINUTE$; : GOTO 3220
3310 IF K$ = "-"             THEN GOSUB 4450 : PRINT MINUTE$; : GOTO 3220
3320 IF K$ = DLT$ THEN GOTO 3000
3330 IF K$ = ESC$ THEN RETURN
3340 IF K$ = RET$ THEN RETURN
3350 IF K$ = "/" OR K$ = "?" THEN GOSUB 580 : GOTO 3220
3360 GOSUB 1340
3370 GOTO 3220
3380 REM->MINUTEDIGITTWO
3390 HTAB TX+4 : VTAB TY
3400 GET K$
3410 IF K$ < "0" OR "9" < K$ THEN GOTO 3470
3420 LET FIRST$ = LEFT$(MINUTE$, 1)
3430 LET TEST$ = FIRST$ + K$
3440 LET TV = VAL(TEST$)
3450 IF 0 <= TV AND TV <= 59 THEN PRINT K$; : MINUTE$ = TEST$ : MINUTE = TV : GOTO 3610
3460 REM->MINUTEDIGITTWOKEYS
3470 IF K$ = " "             THEN GOTO 3610
3480 IF K$ = TB$        THEN GOTO 3610
3490 IF K$ = RT$ THEN GOTO 3610
3500 IF K$ = LT$  THEN GOTO 3220
3510 IF K$ = UP$    THEN GOTO 2020
3520 IF K$ = "+"             THEN GOSUB 4380 : PRINT LT$;MINUTE$; : GOTO 3390
3530 IF K$ = "-"             THEN GOSUB 4450 : PRINT LT$;MINUTE$; : GOTO 3390
3540 IF K$ = DLT$ THEN GOTO 3220
3550 IF K$ = ESC$ THEN RETURN
3560 IF K$ = RET$ THEN RETURN
3570 IF K$ = "/" OR K$ = "?" THEN GOSUB 580 : GOTO 3390
3580 GOSUB 1340
3590 GOTO 3390
3600 REM->MERIDIEM
3610 HTAB TX+6 : VTAB TY
3620 GET K$
3630 IF K$ = "A" OR K$ = "P" THEN PRINT K$; : AMPM$ = K$ : GOTO 1380
3640 IF K$ = " "             THEN GOTO 1380
3650 IF K$ = TB$        THEN GOTO 1380
3660 IF K$ = RT$ THEN GOTO 1380
3670 IF K$ = LT$  THEN GOTO 3390
3680 IF K$ = UP$    THEN GOTO 2440
3690 IF K$ = "+"             THEN GOSUB 4520 : PRINT AMPM$; : GOTO 3610
3700 IF K$ = "-"             THEN GOSUB 4560 : PRINT AMPM$; : GOTO 3610
3710 IF K$ = DLT$ THEN GOTO 3390
3720 IF K$ = ESC$ THEN RETURN
3730 IF K$ = RET$ THEN RETURN
3740 IF K$ = "/" OR K$ = "?" THEN GOSUB 580 : GOTO 3610
3750 GOSUB 1340
3760 GOTO 3610
3770 REM->PADYEAR
3780 IF YEAR = 0 THEN YEAR$ = "0000": RETURN 
3790 LET YEAR$ = STR$(2000 + YEAR)
3800 RETURN 
3810 REM->INCDAY
3820 LET DAY = VAL(DAY$)
3830 LET DAY = DAY + 1
3840 GOSUB 880
3850 IF DAY > MDAYS(MNTH) THEN DAY = 1
3860 LET DAY$ = STR$(DAY)
3870 IF DAY < 10 THEN DAY$ = "0" + DAY$
3880 RETURN
3890 REM->DECDAY
3900 LET DAY = VAL(DAY$)
3910 LET DAY = DAY - 1
3920 GOSUB 880
3930 IF DAY <= 0 THEN DAY = MDAYS(MNTH)
3940 LET DAY$ = STR$(DAY)
3950 IF DAY < 10 THEN DAY$ = "0" + DAY$
3960 RETURN
3970 REM->INCMONTH
3980 GOSUB 880
3990 LET MNTH = MNTH - 1
4000 IF MNTH < 1 THEN MNTH = 12
4010 GOSUB 770
4020 RETURN
4030 REM->DECMONTH
4040 GOSUB 880
4050 LET MNTH = MNTH + 1
4060 IF MNTH > 12 THEN MNTH = 1
4070 GOSUB 770
4080 RETURN
4090 REM->INCYEAR
4100 LET YEAR = VAL(YEAR$)
4110 LET YEAR = YEAR + 1
4120 IF YEAR > 99 THEN YEAR = 0
4130 LET YEAR$ = STR$(YEAR)
4140 IF YEAR < 10 THEN YEAR$ = "0" + YEAR$
4150 RETURN
4160 REM->DECYEAR
4170 LET YEAR = VAL(YEAR$)
4180 LET YEAR = YEAR - 1
4190 IF YEAR < 0  THEN YEAR = 99
4200 LET YEAR$ = STR$(YEAR)
4210 IF YEAR < 10 THEN YEAR$ = "0" + YEAR$
4220 RETURN
4230 REM->INCHOUR
4240 LET HOUR = VAL(HOUR$)
4250 LET HOUR = HOUR + 1
4260 IF HOUR > 12 THEN HOUR = 1
4270 LET HOUR$ = STR$(HOUR)
4280 IF HOUR < 10 THEN HOUR$ = "0" + HOUR$
4290 RETURN
4300 REM->DECHOUR
4310 LET HOUR = VAL(HOUR$)
4320 LET HOUR = HOUR - 1
4330 IF HOUR < 1 THEN HOUR = 12
4340 LET HOUR$ = STR$(HOUR)
4350 IF HOUR < 10 THEN HOUR$ = "0" + HOUR$
4360 RETURN
4370 REM->INCMINUTE
4380 LET MINUTE = VAL(MINUTE$)
4390 LET MINUTE = MINUTE + 1
4400 IF MINUTE > 59 THEN MINUTE = 0
4410 LET MINUTE$ = STR$(MINUTE)
4420 IF MINUTE < 10 THEN MINUTE$ = "0" + MINUTE$
4430 RETURN
4440 REM->DECMINUTE
4450 LET MINUTE = VAL(MINUTE$)
4460 LET MINUTE = MINUTE - 1
4470 IF MINUTE < 0 THEN MINUTE = 59
4480 LET MINUTE$ = STR$(MINUTE)
4490 IF MINUTE < 10 THEN MINUTE$ = "0" + MINUTE$
4500 RETURN
4510 REM->INCMERIDIEM
4520 IF AMPM$ = "A" THEN AMPM$ = "P" : RETURN
4530 IF AMPM$ = "P" THEN AMPM$ = "A"
4540 RETURN
4550 REM->DECMERIDIEM
4560 IF AMPM$ = "A" THEN AMPM$ = "P" : RETURN
4570 IF AMPM$ = "P" THEN AMPM$ = "A"
4580 RETURN
RUN
