10 REM SETDATE.BAS - 08/02/2019 - 07h27
20 REM ********************************************************
30 REM PROGRAM: SETDATE
40 REM SYSTEM:  PRODOS
50 REM AUTHOR:  BILL CHATFIELD
60 REM LICENSE: GPL3
70 REM SOURCE:  HTTPS://GITHUB.COM/GUNGWALD/SETDATE/SETDATE.BAZ
80 REM ********************************************************
90 LET D$=CHR$(4)
100 LET LT$=CHR$(8)
110 LET DOWN$=CHR$(10)
120 LET UP$=CHR$(11)
130 LET RET$=CHR$(13)
140 LET D1$=CHR$(17) 
150 LET D2$=CHR$(18) 
160 LET NAK$=CHR$(21)   
170 LET RT$=CHR$(21)
180 LET ESC$=CHR$(27)
190 LET DLT$=CHR$(127)
200 LET BEEP$=CHR$(7)
210 LET TB$=CHR$(9)
220 DIM MNTHS$(12), MDAYS(12)
230 FOR I = 1 TO 12
240 READ MNTHS$(I), MDAYS(I)
250 NEXT
260 DATA JAN,31
270 DATA FEB,29
280 DATA MAR,31
290 DATA APR,30
300 DATA MAY,31
310 DATA JUN,30
320 DATA JUL,31
330 DATA AUG,31
340 DATA SEP,30
350 DATA OCT,31
360 DATA NOV,30
370 DATA DEC,31
380 GOSUB 1060:REM GO->GETSYSTEMDATE 
390 GOSUB 810:REM GO->FORMATDATEANDTIME 
400 GOSUB 500:REM GO->DRAWSCREEN 
410 GOSUB 1410:REM GO->ENTERDATEANDTIME 
420 GOSUB 1000:REM GO->DECODEDATEANDTIME 
430 GOSUB 1230:REM GO->SETSYSTEMDATEANDTIME 
440 VTAB TY : POKE 36,38 : PRINT
450 PRINT
460 IF K$ =  RET$ THEN PRINT "SUCCESSFULLY SET SYSTEM DATE AND TIME."
470 IF K$ <> RET$ THEN PRINT "CANCELLED SETTING OF DATE AND TIME."
480 END
490 REM->DRAWSCREEN
500 PRINT "TYPE '?' FOR HELP."
510 PRINT
520 PRINT "DATE: ";DAY$;"-";MNTH$;"-";YEAR$;     "  (DD-MMM-YY)"
530 PRINT "TIME: ";HOUR$;":";MINUTE$;" ";AMPM$;"M"
540 LET TX = 7 : REM TIME HORIZONTAL POSITION
550 LET DX = 7 : REM DATE HORIZONTAL POSITION
560 CALL -998 : REM MOVE CURSOR UP 1 LINE TO TIME LINE
570 LET TY = PEEK(37) + 1 : REM TIME VERTICAL POSITION
580 CALL -998 : REM MOVE CURSOR UP 1 LINE TO DATE LINE
590 LET DY = PEEK(37) + 1 : REM DATE VERTICAL POSITION
600 RETURN
610 REM->DISPLAYHELP
620 VTAB TY : POKE 36,38 : PRINT
630 PRINT
640 PRINT "*** HELP ***"
650 PRINT "RETURN - SET DATA & TIME"
660 PRINT "ESC    - EXIT WITHOUT MAKING CHANGES"
670 PRINT "TAB    - SKIP TO NEXT ELEMENT"
680 PRINT "ARROWS - MOVE ONE CHAR IN ANY DIRECTION"
690 PRINT "SPACE  - MOVE FORWARD ONE CHARACTER"
700 PRINT "DELETE - MOVE BACK ONE CHARACTER"
710 PRINT "+      - INCREMENT CURRENT VALUE"
720 PRINT "-      - DECREMENT CURRENT VALUE"
730 PRINT "?      - DISPLAY HELP"
740 PRINT
750 GOSUB 500:REM GO->DRAWSCREEN 
760 RETURN
770 REM->FORMATMONTH
780 LET MNTH$ = MNTHS$(MNTH)
790 RETURN
800 REM->FORMATDATEANDTIME
810 LET YEAR$ = STR$(YEAR)
820 IF LEN(YEAR$) = 1 THEN LET YEAR$ = "0" + YEAR$
830 GOSUB 780:REM GO->FORMATMONTH 
840 LET DAY$ = STR$(DAY)
850 IF LEN(DAY$) = 1 THEN LET DAY$ = "0" + DAY$
860 LET HOUR$ = STR$(HOUR)
870 IF LEN(HOUR$) = 1 THEN LET HOUR$ = "0" + HOUR$
880 LET MINUTE$ = STR$(MINUTE)
890 IF LEN(MINUTE$) = 1 THEN LET MINUTE$ = "0" + MINUTE$
900 RETURN
910 REM->DECODEMONTH
920 FOR I = 1 TO 12
930 IF MNTHS$(I) = MNTH$ THEN MNTH = I : RETURN
940 NEXT
950 VTAB 24
960 PRINT
970 PRINT "INVALID MONTH NAME: " + MNTH$
980 END
990 REM->DECODEDATEANDTIME
1000 LET YEAR = VAL(YEAR$)
1010 GOSUB 920:REM GO->DECODEMONTH 
1020 LET HOUR = VAL(HOUR$)
1030 LET MINUTE = VAL(MINUTE$)
1040 RETURN
1050 REM->GETSYSTEMDATE
1060 LET DH = PEEK(49041): REM DATE HIGH BYTE
1070 LET DL = PEEK(49040): REM DATE LOW BYTE
1080 LET TH = PEEK(49043): REM TIME HIGH BYTE
1090 LET TL = PEEK(49042): REM TIME LOW BYTE 
1100 LET YEAR = INT (DH / 2)
1110 LET MNTH = ((DH/2 - INT(DH/2)) > 0) * 2 ^ 3 +  INT (DL / 32)
1120 LET DAY = DL
1130 IF DAY >= 128 THEN DAY = DAY - 128
1140 IF DAY >= 64  THEN DAY = DAY - 64
1150 IF DAY >= 32  THEN DAY = DAY - 32
1160 IF TH = 0  THEN HOUR = 12      : AMPM$ = "A"
1170 IF TH = 12 THEN HOUR = 12      : AMPM$ = "P"
1180 IF TH > 12 THEN HOUR = TH - 12 : AMPM$ = "P"
1190 IF TH < 12 THEN HOUR = TH      : AMPM$ = "A"
1200 LET MINUTE = TL
1210 RETURN 
1220 REM->SETSYSTEMDATEANDTIME
1230 IF YEAR >= 100 THEN Y2 = VAL(RIGHT$(STR$(YEAR),2))
1240 IF YEAR <  100 THEN Y2 = YEAR
1250 LET DH = Y2 * 2 + (MNTH >  = 8)
1260 LET DL = MNTH * 32 - 128 * (MNTH >  = 8) + DAY
1270 IF HOUR =  12 AND AMPM$ = "A" THEN TH = 0
1280 IF HOUR =  12 AND AMPM$ = "P" THEN TH = 12
1290 IF HOUR <> 12 AND AMPM$ = "P" THEN TH = HOUR + 12
1300 IF HOUR <> 12 AND AMPM$ = "A" THEN TH = HOUR
1310 LET TL = MINUTE
1320 POKE 49041,DH: REM DATE HIGH BYTE
1330 POKE 49040,DL: REM DATE LOW BYTE
1340 POKE 49043,TH: REM TIME HIGH BYTE
1350 POKE 49042,TL: REM TIME LOW BYTE
1360 RETURN 
1370 REM->SOUNDERROR
1380 PRINT BEEP$
1390 RETURN
1400 REM->ENTERDATEANDTIME
1410 REM->DAYDIGITONE
1420 HTAB DX : VTAB DY
1430 GET K$
1440 IF "0" <= K$ AND K$ <= "3" THEN PRINT K$; : DAY$ = K$ + RIGHT$(DAY$, 1) : GOTO 1590:REM GO->DAYDIGITTWO 
1450 IF K$ = " "             THEN GOTO 1590:REM GO->DAYDIGITTWO 
1460 IF K$ = TB$        THEN GOTO 1810:REM GO->MONTHCHARONE 
1470 IF K$ = RT$ THEN GOTO 1590:REM GO->DAYDIGITTWO 
1480 IF K$ = LT$  THEN GOTO 3650:REM GO->MERIDIEM 
1490 IF K$ = DOWN$ THEN GOTO 2870:REM GO->HOURDIGITONE 
1500 IF K$ = "+"             THEN GOSUB 3860 : PRINT DAY$; : GOTO 1420:REM GO->INCDAY DAYDIGITONE 
1510 IF K$ = "-"             THEN GOSUB 3940 : PRINT DAY$; : GOTO 1420:REM GO->DECDAY DAYDIGITONE 
1520 IF K$ = DLT$ THEN GOTO 3650:REM GO->MERIDIEM 
1530 IF K$ = ESC$ THEN RETURN
1540 IF K$ = RET$ THEN RETURN
1550 IF K$ = "/" OR K$ = "?" THEN GOSUB 620 : GOTO 1420:REM GO->DISPLAYHELP DAYDIGITONE 
1560 GOSUB 1380:REM GO->SOUNDERROR 
1570 GOTO 1420:REM GO->DAYDIGITONE 
1580 REM->DAYDIGITTWO
1590 HTAB DX+1 : VTAB DY
1600 GET K$
1610 IF K$ < "0" OR "9" < K$ THEN GOTO 1670:REM GO->DAYDIGITTWOKEYS 
1620 LET FIRST$ = LEFT$(DAY$, 1)
1630 LET TEST$ = FIRST$ + K$
1640 LET TV = VAL(TEST$)
1650 IF 1 <= TV AND TV <= MDAYS(MNTH) THEN PRINT K$; : DAY$ = TEST$ : DAY = TV : GOTO 1810:REM GO->MONTHCHARONE 
1660 REM->DAYDIGITTWOKEYS
1670 IF K$ = " "             THEN GOTO 1810:REM GO->MONTHCHARONE 
1680 IF K$ = TB$        THEN GOTO 1810:REM GO->MONTHCHARONE 
1690 IF K$ = RT$ THEN GOTO 1810:REM GO->MONTHCHARONE 
1700 IF K$ = LT$  THEN GOTO 1420:REM GO->DAYDIGITONE 
1710 IF K$ = DOWN$ THEN GOTO 3040:REM GO->HOURDIGITTWO 
1720 IF K$ = "+"             THEN GOSUB 3860 : PRINT LT$;DAY$; : GOTO 1590:REM GO->INCDAY DAYDIGITTWO 
1730 IF K$ = "-"             THEN GOSUB 3940 : PRINT LT$;DAY$; : GOTO 1590:REM GO->DECDAY DAYDIGITTWO 
1740 IF K$ = DLT$ THEN GOTO 1420:REM GO->DAYDIGITONE 
1750 IF K$ = ESC$ THEN RETURN
1760 IF K$ = RET$ THEN RETURN
1770 IF K$ = "/" OR K$ = "?" THEN GOSUB 620 : GOTO 1590:REM GO->DISPLAYHELP DAYDIGITTWO 
1780 GOSUB 1380:REM GO->SOUNDERROR 
1790 GOTO 1590:REM GO->DAYDIGITTWO 
1800 REM->MONTHCHARONE
1810 HTAB DX+3 : VTAB DY
1820 GET K$
1830 IF K$ = LEFT$(MNTH$, 1) THEN GOTO 2060:REM GO->MONTHCHARTWO 
1840 IF K$ = "J" THEN MNTH$ = "JAN" : MNTH=1  : PRINT MNTH$; : GOTO 2060:REM GO->MONTHCHARTWO 
1850 IF K$ = "F" THEN MNTH$ = "FEB" : MNTH=2  : PRINT MNTH$; : GOTO 2060:REM GO->MONTHCHARTWO 
1860 IF K$ = "M" THEN MNTH$ = "MAR" : MNTH=3  : PRINT MNTH$; : GOTO 2060:REM GO->MONTHCHARTWO 
1870 IF K$ = "A" THEN MNTH$ = "APR" : MNTH=4  : PRINT MNTH$; : GOTO 2060:REM GO->MONTHCHARTWO 
1880 IF K$ = "S" THEN MNTH$ = "SEP" : MNTH=9  : PRINT MNTH$; : GOTO 2060:REM GO->MONTHCHARTWO 
1890 IF K$ = "O" THEN MNTH$ = "OCT" : MNTH=10 : PRINT MNTH$; : GOTO 2060:REM GO->MONTHCHARTWO 
1900 IF K$ = "N" THEN MNTH$ = "NOV" : MNTH=11 : PRINT MNTH$; : GOTO 2060:REM GO->MONTHCHARTWO 
1910 IF K$ = "D" THEN MNTH$ = "DEC" : MNTH=12 : PRINT MNTH$; : GOTO 2060:REM GO->MONTHCHARTWO 
1920 IF K$ = " "             THEN GOTO 2060:REM GO->MONTHCHARTWO 
1930 IF K$ = TB$        THEN GOTO 2480:REM GO->YEARDIGITONE 
1940 IF K$ = RT$ THEN GOTO 2060:REM GO->MONTHCHARTWO 
1950 IF K$ = LT$  THEN GOTO 1590:REM GO->DAYDIGITTWO 
1960 IF K$ = DOWN$ THEN GOSUB 3260:REM GO->MINUTEDIGITONE 
1970 IF K$ = "+"             THEN GOSUB 4020 : PRINT MNTH$; : GOTO 1810:REM GO->INCMONTH MONTHCHARONE 
1980 IF K$ = "-"             THEN GOSUB 4080 : PRINT MNTH$; : GOTO 1810:REM GO->DECMONTH MONTHCHARONE 
1990 IF K$ = DLT$ THEN GOTO 1590:REM GO->DAYDIGITTWO 
2000 IF K$ = ESC$ THEN RETURN
2010 IF K$ = RET$ THEN RETURN
2020 IF K$ = "/" OR K$ = "?" THEN GOSUB 620 : GOTO 1810:REM GO->DISPLAYHELP MONTHCHARONE 
2030 GOSUB 1380:REM GO->SOUNDERROR 
2040 GOTO 1810:REM GO->MONTHCHARONE 
2050 REM->MONTHCHARTWO
2060 HTAB DX+4 : VTAB DY
2070 GET K$
2080 IF K$ = MID$(MNTH$, 2, 1) THEN GOTO 2260:REM GO->MONTHCHARTHREE 
2090 LET FIRST$ = LEFT$(MNTH$, 1)
2100 IF FIRST$ = "A" AND K$ = "P" THEN MNTH$ = "APR" : MNTH=4 : PRINT "PR"; : GOTO 2260:REM GO->MONTHCHARTHREE 
2110 IF FIRST$ = "A" AND K$ = "U" THEN MNTH$ = "AUG" : MNTH=8 : PRINT "UG"; : GOTO 2260:REM GO->MONTHCHARTHREE 
2120 IF K$ = " "             THEN GOTO 2260:REM GO->MONTHCHARTHREE 
2130 IF K$ = TB$        THEN GOTO 2480:REM GO->YEARDIGITONE 
2140 IF K$ = RT$ THEN GOTO 2260:REM GO->MONTHCHARTHREE 
2150 IF K$ = LT$  THEN GOTO 1810:REM GO->MONTHCHARONE 
2160 IF K$ = DOWN$ THEN GOTO 3430:REM GO->MINUTEDIGITTWO 
2170 IF K$ = "+"             THEN GOSUB 4020 : PRINT LT$;MNTH$; : GOTO 2060:REM GO->INCMONTH MONTHCHARTWO 
2180 IF K$ = "-"             THEN GOSUB 4080 : PRINT LT$;MNTH$; : GOTO 2060:REM GO->DECMONTH MONTHCHARTWO 
2190 IF K$ = DLT$ THEN GOTO 1810:REM GO->MONTHCHARONE 
2200 IF K$ = ESC$ THEN RETURN
2210 IF K$ = RET$ THEN RETURN
2220 IF K$ = "/" OR K$ = "?" THEN GOSUB 620 : GOTO 2060:REM GO->DISPLAYHELP MONTHCHARTWO 
2230 GOSUB 1380:REM GO->SOUNDERROR 
2240 GOTO 2060:REM GO->MONTHCHARTWO 
2250 REM->MONTHCHARTHREE
2260 HTAB DX+5 : VTAB DY
2270 IF K$ = RIGHT$(MNTH$, 1) THEN GOTO 2480:REM GO->YEARDIGITONE 
2280 LET TWO$ = LEFT$(MNTH$, 2)
2290 GET K$
2300 IF TWO$ = "MA" AND K$ = "R" THEN MNTH$ = "MAR" : MNTH=3 : PRINT K$; : GOTO 2480:REM GO->YEARDIGITONE 
2310 IF TWO$ = "MA" AND K$ = "Y" THEN MNTH$ = "MAY" : MNTH=5 : PRINT K$; : GOTO 2480:REM GO->YEARDIGITONE 
2320 IF TWO$ = "JU" AND K$ = "N" THEN MNTH$ = "JUN" : MNTH=6 : PRINT K$; : GOTO 2480:REM GO->YEARDIGITONE 
2330 IF TWO$ = "JU" AND K$ = "L" THEN MNTH$ = "JUL" : MNTH=7 : PRINT K$; : GOTO 2480:REM GO->YEARDIGITONE 
2340 IF K$ = " "             THEN GOTO 2480:REM GO->YEARDIGITONE 
2350 IF K$ = TB$        THEN GOTO 2480:REM GO->YEARDIGITONE 
2360 IF K$ = RT$ THEN GOTO 2480:REM GO->YEARDIGITONE 
2370 IF K$ = LT$  THEN GOTO 2060:REM GO->MONTHCHARTWO 
2380 IF K$ = DOWN$ THEN GOTO 3650:REM GO->MERIDIEM 
2390 IF K$ = "+"             THEN GOSUB 4020 : PRINT LT$;LT$;MNTH$; : GOTO 2260:REM GO->INCMONTH MONTHCHARTHREE 
2400 IF K$ = "-"             THEN GOSUB 4080 : PRINT LT$;LT$;MNTH$; : GOTO 2260:REM GO->DECMONTH MONTHCHARTHREE 
2410 IF K$ = DLT$ THEN GOTO 2060:REM GO->MONTHCHARTWO 
2420 IF K$ = ESC$ THEN RETURN
2430 IF K$ = RET$ THEN RETURN
2440 IF K$ = "/" OR K$ = "?" THEN GOSUB 620 : GOTO 2260:REM GO->DISPLAYHELP MONTHCHARTHREE 
2450 GOSUB 1380:REM GO->SOUNDERROR 
2460 GOTO 2260:REM GO->MONTHCHARTHREE 
2470 REM->YEARDIGITONE
2480 HTAB DX+7 : VTAB DY
2490 GET K$
2500 IF "0" <= K$ AND K$ <= "9" THEN PRINT K$; : YEAR$ = K$ + RIGHT$(YEAR$, 1) : GOTO 2650:REM GO->YEARDIGITTWO 
2510 IF K$ = " "             THEN GOTO 2650:REM GO->YEARDIGITTWO 
2520 IF K$ = TB$        THEN GOTO 2870:REM GO->HOURDIGITONE 
2530 IF K$ = RT$ THEN GOTO 2650:REM GO->YEARDIGITTWO 
2540 IF K$ = LT$  THEN GOTO 2260:REM GO->MONTHCHARTHREE 
2550 IF K$ = DOWN$ THEN GOTO 3650:REM GO->MERIDIEM 
2560 IF K$ = "+"             THEN GOSUB 4140 : PRINT YEAR$; : GOTO 2480:REM GO->INCYEAR YEARDIGITONE 
2570 IF K$ = "-"             THEN GOSUB 4210 : PRINT YEAR$; : GOTO 2480:REM GO->DECYEAR YEARDIGITONE 
2580 IF K$ = DLT$ THEN GOTO 2260:REM GO->MONTHCHARTHREE 
2590 IF K$ = ESC$ THEN RETURN
2600 IF K$ = RET$ THEN RETURN
2610 IF K$ = "/" OR K$ = "?" THEN GOSUB 620 : GOTO 2480:REM GO->DISPLAYHELP YEARDIGITONE 
2620 GOSUB 1380:REM GO->SOUNDERROR 
2630 GOTO 2480:REM GO->YEARDIGITONE 
2640 REM->YEARDIGITTWO
2650 HTAB DX+8 : VTAB DY
2660 GET K$
2670 IF K$ < "0" OR "9" < K$ THEN GOTO 2730:REM GO->YEARDIGITTWOKEYS 
2680 LET FIRST$ = LEFT$(YEAR$, 1)
2690 LET TEST$ = FIRST$ + K$
2700 LET TV = VAL(TEST$)
2710 IF 1 <= TV AND TV <= 99 THEN PRINT K$; : YEAR$ = TEST$ : YEAR = TV : GOTO 2870:REM GO->HOURDIGITONE 
2720 REM->YEARDIGITTWOKEYS
2730 IF K$ = " "             THEN GOTO 2870:REM GO->HOURDIGITONE 
2740 IF K$ = TB$        THEN GOTO 2870:REM GO->HOURDIGITONE 
2750 IF K$ = RT$ THEN GOTO 2870:REM GO->HOURDIGITONE 
2760 IF K$ = LT$  THEN GOTO 2480:REM GO->YEARDIGITONE 
2770 IF K$ = DOWN$ THEN GOTO 3650:REM GO->MERIDIEM 
2780 IF K$ = "+"             THEN GOSUB 4140 : PRINT LT$;YEAR$; : GOTO 2650:REM GO->INCYEAR YEARDIGITTWO 
2790 IF K$ = "-"             THEN GOSUB 4210 : PRINT LT$;YEAR$; : GOTO 2650:REM GO->DECYEAR YEARDIGITTWO 
2800 IF K$ = DLT$ THEN GOTO 2480:REM GO->YEARDIGITONE 
2810 IF K$ = ESC$ THEN RETURN
2820 IF K$ = RET$ THEN RETURN
2830 IF K$ = "/" OR K$ = "?" THEN GOSUB 620 : GOTO 2650:REM GO->DISPLAYHELP YEARDIGITTWO 
2840 GOSUB 1380:REM GO->SOUNDERROR 
2850 GOTO 2650:REM GO->YEARDIGITTWO 
2860 REM->HOURDIGITONE
2870 HTAB TX : VTAB TY
2880 GET K$
2890 IF "0" <= K$ AND K$ <= "1" THEN PRINT K$; : HOUR$ = K$ + RIGHT$(HOUR$, 1) : GOTO 3040:REM GO->HOURDIGITTWO 
2900 IF K$ = " "             THEN GOTO 3040:REM GO->HOURDIGITTWO 
2910 IF K$ = TB$        THEN GOTO 3260:REM GO->MINUTEDIGITONE 
2920 IF K$ = RT$ THEN GOTO 3040:REM GO->HOURDIGITTWO 
2930 IF K$ = LT$  THEN GOTO 2650:REM GO->YEARDIGITTWO 
2940 IF K$ = UP$    THEN GOTO 1420:REM GO->DAYDIGITONE 
2950 IF K$ = "+"             THEN GOSUB 4280 : PRINT HOUR$; : GOTO 2870:REM GO->INCHOUR HOURDIGITONE 
2960 IF K$ = "-"             THEN GOSUB 4350 : PRINT HOUR$; : GOTO 2870:REM GO->DECHOUR HOURDIGITONE 
2970 IF K$ = DLT$ THEN GOTO 2650:REM GO->YEARDIGITTWO 
2980 IF K$ = ESC$ THEN RETURN
2990 IF K$ = RET$ THEN RETURN
3000 IF K$ = "/" OR K$ = "?" THEN GOSUB 620 : GOTO 2870:REM GO->DISPLAYHELP HOURDIGITONE 
3010 GOSUB 1380:REM GO->SOUNDERROR 
3020 GOTO 2870:REM GO->HOURDIGITONE 
3030 REM->HOURDIGITTWO
3040 HTAB TX+1 : VTAB TY
3050 GET K$
3060 IF K$ < "0" OR "9" < K$ THEN GOTO 3120:REM GO->HOURDIGITTWOKEYS 
3070 LET FIRST$ = LEFT$(HOUR$, 1)
3080 LET TEST$ = FIRST$ + K$
3090 LET TV = VAL(TEST$)
3100 IF 1 <= TV AND TV <= 12 THEN PRINT K$; : HOUR$ = TEST$ : HOUR = TV : GOTO 3260:REM GO->MINUTEDIGITONE 
3110 REM->HOURDIGITTWOKEYS
3120 IF K$ = " "             THEN GOTO 3260:REM GO->MINUTEDIGITONE 
3130 IF K$ = TB$        THEN GOTO 3260:REM GO->MINUTEDIGITONE 
3140 IF K$ = RT$ THEN GOTO 3260:REM GO->MINUTEDIGITONE 
3150 IF K$ = LT$  THEN GOTO 2870:REM GO->HOURDIGITONE 
3160 IF K$ = UP$    THEN GOTO 1590:REM GO->DAYDIGITTWO 
3170 IF K$ = "+"             THEN GOSUB 4280 : PRINT LT$;HOUR$; : GOTO 3040:REM GO->INCHOUR HOURDIGITTWO 
3180 IF K$ = "-"             THEN GOSUB 4350 : PRINT LT$;HOUR$; : GOTO 3040:REM GO->DECHOUR HOURDIGITTWO 
3190 IF K$ = DLT$ THEN GOTO 2870:REM GO->HOURDIGITONE 
3200 IF K$ = ESC$ THEN RETURN
3210 IF K$ = RET$ THEN RETURN
3220 IF K$ = "/" OR K$ = "?" THEN GOSUB 620 : GOTO 3040:REM GO->DISPLAYHELP HOURDIGITTWO 
3230 GOSUB 1380:REM GO->SOUNDERROR 
3240 GOTO 3040:REM GO->HOURDIGITTWO 
3250 REM->MINUTEDIGITONE
3260 HTAB TX+3 : VTAB TY
3270 GET K$
3280 IF "0" <= K$ AND K$ <= "5" THEN PRINT K$; : MINUTE$ = K$ + RIGHT$(MINUTE$, 1) : GOTO 3430:REM GO->MINUTEDIGITTWO 
3290 IF K$ = " "             THEN GOTO 3430:REM GO->MINUTEDIGITTWO 
3300 IF K$ = TB$        THEN GOTO 3650:REM GO->MERIDIEM 
3310 IF K$ = RT$ THEN GOTO 3430:REM GO->MINUTEDIGITTWO 
3320 IF K$ = LT$  THEN GOTO 3040:REM GO->HOURDIGITTWO 
3330 IF K$ = UP$    THEN GOTO 1810:REM GO->MONTHCHARONE 
3340 IF K$ = "+"             THEN GOSUB 4420 : PRINT MINUTE$; : GOTO 3260:REM GO->INCMINUTE MINUTEDIGITONE 
3350 IF K$ = "-"             THEN GOSUB 4490 : PRINT MINUTE$; : GOTO 3260:REM GO->DECMINUTE MINUTEDIGITONE 
3360 IF K$ = DLT$ THEN GOTO 3040:REM GO->HOURDIGITTWO 
3370 IF K$ = ESC$ THEN RETURN
3380 IF K$ = RET$ THEN RETURN
3390 IF K$ = "/" OR K$ = "?" THEN GOSUB 620 : GOTO 3260:REM GO->DISPLAYHELP MINUTEDIGITONE 
3400 GOSUB 1380:REM GO->SOUNDERROR 
3410 GOTO 3260:REM GO->MINUTEDIGITONE 
3420 REM->MINUTEDIGITTWO
3430 HTAB TX+4 : VTAB TY
3440 GET K$
3450 IF K$ < "0" OR "9" < K$ THEN GOTO 3510:REM GO->MINUTEDIGITTWOKEYS 
3460 LET FIRST$ = LEFT$(MINUTE$, 1)
3470 LET TEST$ = FIRST$ + K$
3480 LET TV = VAL(TEST$)
3490 IF 0 <= TV AND TV <= 59 THEN PRINT K$; : MINUTE$ = TEST$ : MINUTE = TV : GOTO 3650:REM GO->MERIDIEM 
3500 REM->MINUTEDIGITTWOKEYS
3510 IF K$ = " "             THEN GOTO 3650:REM GO->MERIDIEM 
3520 IF K$ = TB$        THEN GOTO 3650:REM GO->MERIDIEM 
3530 IF K$ = RT$ THEN GOTO 3650:REM GO->MERIDIEM 
3540 IF K$ = LT$  THEN GOTO 3260:REM GO->MINUTEDIGITONE 
3550 IF K$ = UP$    THEN GOTO 2060:REM GO->MONTHCHARTWO 
3560 IF K$ = "+"             THEN GOSUB 4420 : PRINT LT$;MINUTE$; : GOTO 3430:REM GO->INCMINUTE MINUTEDIGITTWO 
3570 IF K$ = "-"             THEN GOSUB 4490 : PRINT LT$;MINUTE$; : GOTO 3430:REM GO->DECMINUTE MINUTEDIGITTWO 
3580 IF K$ = DLT$ THEN GOTO 3260:REM GO->MINUTEDIGITONE 
3590 IF K$ = ESC$ THEN RETURN
3600 IF K$ = RET$ THEN RETURN
3610 IF K$ = "/" OR K$ = "?" THEN GOSUB 620 : GOTO 3430:REM GO->DISPLAYHELP MINUTEDIGITTWO 
3620 GOSUB 1380:REM GO->SOUNDERROR 
3630 GOTO 3430:REM GO->MINUTEDIGITTWO 
3640 REM->MERIDIEM
3650 HTAB TX+6 : VTAB TY
3660 GET K$
3670 IF K$ = "A" OR K$ = "P" THEN PRINT K$; : AMPM$ = K$ : GOTO 1420:REM GO->DAYDIGITONE 
3680 IF K$ = " "             THEN GOTO 1420:REM GO->DAYDIGITONE 
3690 IF K$ = TB$        THEN GOTO 1420:REM GO->DAYDIGITONE 
3700 IF K$ = RT$ THEN GOTO 1420:REM GO->DAYDIGITONE 
3710 IF K$ = LT$  THEN GOTO 3430:REM GO->MINUTEDIGITTWO 
3720 IF K$ = UP$    THEN GOTO 2480:REM GO->YEARDIGITONE 
3730 IF K$ = "+"             THEN GOSUB 4560 : PRINT AMPM$; : GOTO 3650:REM GO->INCMERIDIEM MERIDIEM 
3740 IF K$ = "-"             THEN GOSUB 4600 : PRINT AMPM$; : GOTO 3650:REM GO->DECMERIDIEM MERIDIEM 
3750 IF K$ = DLT$ THEN GOTO 3430:REM GO->MINUTEDIGITTWO 
3760 IF K$ = ESC$ THEN RETURN
3770 IF K$ = RET$ THEN RETURN
3780 IF K$ = "/" OR K$ = "?" THEN GOSUB 620 : GOTO 3650:REM GO->DISPLAYHELP MERIDIEM 
3790 GOSUB 1380:REM GO->SOUNDERROR 
3800 GOTO 3650:REM GO->MERIDIEM 
3810 REM->PADYEAR
3820 IF YEAR = 0 THEN YEAR$ = "0000": RETURN 
3830 LET YEAR$ = STR$(2000 + YEAR)
3840 RETURN 
3850 REM->INCDAY
3860 LET DAY = VAL(DAY$)
3870 LET DAY = DAY + 1
3880 GOSUB 920:REM GO->DECODEMONTH 
3890 IF DAY > MDAYS(MNTH) THEN DAY = 1
3900 LET DAY$ = STR$(DAY)
3910 IF DAY < 10 THEN DAY$ = "0" + DAY$
3920 RETURN
3930 REM->DECDAY
3940 LET DAY = VAL(DAY$)
3950 LET DAY = DAY - 1
3960 GOSUB 920:REM GO->DECODEMONTH 
3970 IF DAY <= 0 THEN DAY = MDAYS(MNTH)
3980 LET DAY$ = STR$(DAY)
3990 IF DAY < 10 THEN DAY$ = "0" + DAY$
4000 RETURN
4010 REM->INCMONTH
4020 GOSUB 920:REM GO->DECODEMONTH 
4030 LET MNTH = MNTH - 1
4040 IF MNTH < 1 THEN MNTH = 12
4050 GOSUB 810:REM GO->FORMATDATEANDTIME 
4060 RETURN
4070 REM->DECMONTH
4080 GOSUB 920:REM GO->DECODEMONTH 
4090 LET MNTH = MNTH + 1
4100 IF MNTH > 12 THEN MNTH = 1
4110 GOSUB 810:REM GO->FORMATDATEANDTIME 
4120 RETURN
4130 REM->INCYEAR
4140 LET YEAR = VAL(YEAR$)
4150 LET YEAR = YEAR + 1
4160 IF YEAR > 99 THEN YEAR = 0
4170 LET YEAR$ = STR$(YEAR)
4180 IF YEAR < 10 THEN YEAR$ = "0" + YEAR$
4190 RETURN
4200 REM->DECYEAR
4210 LET YEAR = VAL(YEAR$)
4220 LET YEAR = YEAR - 1
4230 IF YEAR < 0  THEN YEAR = 99
4240 LET YEAR$ = STR$(YEAR)
4250 IF YEAR < 10 THEN YEAR$ = "0" + YEAR$
4260 RETURN
4270 REM->INCHOUR
4280 LET HOUR = VAL(HOUR$)
4290 LET HOUR = HOUR + 1
4300 IF HOUR > 12 THEN HOUR = 1
4310 LET HOUR$ = STR$(HOUR)
4320 IF HOUR < 10 THEN HOUR$ = "0" + HOUR$
4330 RETURN
4340 REM->DECHOUR
4350 LET HOUR = VAL(HOUR$)
4360 LET HOUR = HOUR - 1
4370 IF HOUR < 1 THEN HOUR = 12
4380 LET HOUR$ = STR$(HOUR)
4390 IF HOUR < 10 THEN HOUR$ = "0" + HOUR$
4400 RETURN
4410 REM->INCMINUTE
4420 LET MINUTE = VAL(MINUTE$)
4430 LET MINUTE = MINUTE + 1
4440 IF MINUTE > 59 THEN MINUTE = 0
4450 LET MINUTE$ = STR$(MINUTE)
4460 IF MINUTE < 10 THEN MINUTE$ = "0" + MINUTE$
4470 RETURN
4480 REM->DECMINUTE
4490 LET MINUTE = VAL(MINUTE$)
4500 LET MINUTE = MINUTE - 1
4510 IF MINUTE < 0 THEN MINUTE = 59
4520 LET MINUTE$ = STR$(MINUTE)
4530 IF MINUTE < 10 THEN MINUTE$ = "0" + MINUTE$
4540 RETURN
4550 REM->INCMERIDIEM
4560 IF AMPM$ = "A" THEN AMPM$ = "P" : RETURN
4570 IF AMPM$ = "P" THEN AMPM$ = "A"
4580 RETURN
4590 REM->DECMERIDIEM
4600 IF AMPM$ = "A" THEN AMPM$ = "P" : RETURN
4610 IF AMPM$ = "P" THEN AMPM$ = "A"
4620 RETURN